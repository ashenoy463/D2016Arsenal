ACTOR D4MP40 : D4WeaponBase replaces MP40
{
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 20
	Weapon.AmmoType2 "D4MP40AmmoDual"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "Clip1"
	Obituary "%o was shot down by %k's MP40."
    AttackSound "None"
    Inventory.PickupSound "CLIPIN"
	Inventory.Pickupmessage "You got the MP40 (Slot 2)!"
    +WEAPON.NOAUTOAIM
    +WEAPON.NOALERT
	+WEAPON.NOAUTOFIRE
	+WEAPON.CHEATNOTWEAPON
	Weapon.SelectionOrder 500
    Scale 1.0
	Tag "MP-40"
	States
	{		
	NoAmmo:
		RIFG A 0
		RIFG A 0 A_ZoomFactor(1.0)
		RIFG A 0 A_Takeinventory("Zoomed",1)
		RIFG A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_PlaySound("weapons/empty", 4)
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, "WReady")
        TNT1 A 0 A_JumpIfInventory("Clip1",1,"Reload")
		Goto WReady
		
	WReady:
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ReadyZoomed")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"ReadyDual")
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntR")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Unloading",1,"Unload")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		TNT1 A 0 A_JumpIfInventory("HasUnloaded_MP40",1,"ReadyUnloaded")
		MP40 A 1 A_WeaponReady
		Loop
		
	ReadyUnloaded:
		MP4U C 1 A_WeaponReady
		Goto WReady
		
	ReadyZoomed:
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"NoAim")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"NoAim")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"NoAim")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"NoAim")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "NoAim")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "NoAim")
		TNT1 A 0 A_JumpIfInventory("Unloading",1,"NoAim")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"NoAim")
		MP41 A 1 A_WeaponReady
		Loop
		
	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("SubMachineGunSelected",1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		MP4S A 0
		Goto SelectSequence	
		
	Ready:
		TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlaySound("CLIPIN")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"SelectDual")
        MP4S A 0
		Goto RealSelect
		SelectDual:
			M42S A 0
			Goto RealSelect
			
	RealSelect:
		"####" ABCDE 1
		TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady

	Deselect:
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1) 
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"DeselectDual")
        MP4S A 0
		Goto RealDeselect
		DeselectDual:
			M42S A 0
			Goto RealDeselect
	RealDeselect:
        "####" EDCBA 1
		"####" A 0 A_Takeinventory("MP40DualWield",1) 
		Goto DeselectSequence
		
    Fire:
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"FireDual")
		TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",1,1)
        Goto Reload
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"Fire2")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
        TNT1 A 0 A_PlaySound("MP40", 1)
		TNT1 A 0 A_PlaySound("FARSHT", 6)
		TNT1 A 0 A_Takeinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("D4MP40AmmoDual",1)
		MP40 B 1 BRIGHT A_AlertMonsters
		MP40 C 1 A_FireBullets (3, 3, -1, 15, "WeakPuff", FBF_NORANDOM)
		TNT1 A 0 A_ZoomFactor(0.99)
		MP40 D 1 A_FireCustomMissile("PistolCaseSpawn",-5,0,8,-4)
        TNT1 A 0 A_ZoomFactor(1.0)
		MP40 E 1
        TNT1 A 0 A_Refire
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady

	Fire2:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("MP40", 1)
		TNT1 A 0 A_PlaySound("FARSHT", 6)
		TNT1 A 0 A_Takeinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("D4MP40AmmoDual",1)
		MP41 B 1 BRIGHT A_AlertMonsters
		TNT1 A 0 A_FireBullets (1.5, 1.5, -1, 15, "WeakPuff", FBF_NORANDOM)
		MP41 C 1 A_ZoomFactor(1.28)
		MP41 D 1 A_FireCustomMissile("PistolCaseSpawn",-5,0,8,-4)
		TNT1 A 0
        MP41 E 1 A_ZoomFactor(1.3)
		TNT1 A 0
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP40 A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")//If still pressing alt fire, it means its holding aim
        TNT1 A 0 A_Refire
		Goto WReady
		
	AltFire:
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"FireDual")
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
        TNT1 A 0 A_ZoomFactor(1.3)
		TNT1 A 0 A_Giveinventory("ADSmode",1)
        MP4D CBA 1
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 2 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP40 A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")
        Goto WReady
		
	HoldAim:	
	    MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredPrimary",1,"Fire")
		MP41 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		MP40 A 0 A_JumpIfInventory("FiredSecondary",1,"HoldAim")		
	NoAim:
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		MP4D ABC 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
        Goto WReady
		
	Reload:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"ReloadDual")
		TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",32,"WReady")
        TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
        Goto NoAmmo
		TNT1 A 0
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_PlaySound("Reload")
		TNT1 A 0 A_GiveInventory ("Pumping", 1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",1,"ReloadWhitABullet") // If You Have a one Bullet
		TNT1 A 0 A_JumpIfInventory("HasUnloaded_MP40", 1, "ReloadUnloaded") // If You Have Unloaded Gun
		MP4R ABJCDEF 1
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-4)
		MP4R GGGGFHICC 1
		TNT1 A 0 A_Takeinventory("Reloading",1)		
		Goto InsertBullets
		
	ReloadWhitABullet:
		MP4R ABCIHF 1 
        MP4R GGGGGGGGGGGGGGGFHIIICCBA 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto InsertBullets2
		
	ReloadUnloaded:
		MP4U CBA 2
		MP4R GGGGFHICC 1
		Goto InsertBullets
		
	InsertBullets:
		TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",32,"ReloadEnd")
		TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
		Goto ReloadEnd
		TNT1 A 0
		TNT1 A 0 A_Giveinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Giveinventory("D4MP40AmmoDual",1)
		TNT1 A 0 A_Takeinventory("Clip1",1)
		Loop
		
	ReloadEnd:
		MP4R CKLM 2
		TNT1 A 0 A_PlaySound("SLAPBABY")
		MP4R NOOPQRL 2
		MP4R KCJBA 1
		TNT1 A 0 A_Takeinventory("HasUnloaded_MP40",1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
				
	InsertBullets2:
		TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",32,"WReady")
		TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
		Goto WReady
		TNT1 A 0 A_Giveinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Giveinventory("D4MP40AmmoDual",1)
		TNT1 A 0 A_Takeinventory("Clip1",1)
		Loop
		
	Unload:
		TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"UnloadDual")
        TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",1,1)
        Goto NoAmmo
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_PlaySound("Reload")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		MP4R ABJCHIIFFFGGGG 1
		TNT1 A 0 A_GiveInventory ("Pumping", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
	RemoveBullets:
		TNT1 A 0 A_JumpIfInventory("D4MP40Ammo",1,1)
		Goto FinishUnload
		TNT1 A 0 A_Takeinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("D4MP40AmmoDual",1)
		TNT1 A 0 A_Giveinventory("Clip1",1)
		Goto RemoveBullets
	
	FInishUnload:
		MP4U ABC 2
		TNT1 A 0 A_PlaySound("DryFire")
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_TakeInventory("HasUnloaded_MP40", 1)
		TNT1 A 0 A_Takeinventory("Unloading",1)
		Goto WReady

 	Spawn:
		MP40 I 1
		TNT1 A 0 A_SpawnItemEx("D4MP40NoAmmo", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY)
		Stop
		
// Dual Wielding
	DualWield:
		TNT1 A 0 A_Takeinventory("StartDualWield",1)
		TNT1 A 0 A_JumpIfInventory("MP40DualWieldEnabler",1,1)
		Goto FailDualWield
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"UnDual")
		MP4S EDCBA 1
		TNT1 A 4
		TNT1 A 0 A_Giveinventory("MP40DualWield",1)
		Goto SelectAnimation
	UnDual:
		M42S EDCBA 1
		TNT1 A 4
		TNT1 A 0 A_Takeinventory("MP40DualWield",1)
		Goto SelectAnimation

	ReadyDual:
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickDualWield")
        TNT1 A 0 A_TakeInventory("Taunting",1)
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("Unloading",1,"Unload")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"Reload")
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		MP42 A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto WReady
	
    FireDual:
		MP42 A 0 A_JumpIfInventory("D4MP40Ammo",1,1)//Checks if single gun is not depleted
        Goto FireDual2
        TNT1 A 0 A_GunFlash("PrimaryDualWield")
		MP42 B 1 BRIGHT
        MP42 C 1 A_FireCustomMissile("PistolCaseSpawn",5,0,6,-2)	
		TNT1 A 0 A_Takeinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("D4MP40AmmoDual",1)
		MP42 A 0 A_JumpIfInventory("D4MP40AmmoDual",1,"FireDual2")
        Goto FireEndDual
	FireDual2:
		TNT1 A 0 A_JumpIfInventory("D4MP40AmmoDual",1,1)
        Goto NoAmmo
        TNT1 A 0 A_GunFlash("PrimaryDualWield")
		MP42 D 1 BRIGHT
        MP42 E 1 A_FireCustomMissile("PistolCaseSpawn",5,0,-8,-2)
		TNT1 A 0 A_Takeinventory("D4MP40AmmoDual",1)
	FireEndDual:
		TNT1 A 0 A_ReFire("FireDual")
		MP42 A 1 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
		MP42 A 7 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	PrimaryDualWield:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("FARSHT", 6)
        TNT1 A 0 A_PlaySound("MP40", 1)
		TNT1 A 0 A_ZoomFactor(0.99)
		TNT1 A 1 BRIGHT A_FireBullets (4, 3, -1, 15, "WeakPuff", FBF_NORANDOM)
		TNT1 A 0 A_ZoomFactor(1.0)
		Goto Flash
	
    ReloadDual:
		TNT1 A 0 A_JumpIfInventory("D4MP40AmmoDual",64,"WReady")
        TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
        Goto NoAmmo
		TNT1 A 0
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory ("Pumping", 1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		RIFR C 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-4)
		MP42 F 1
		TNT1 A 5
		TNT1 A 0 A_PlaySound("reload",1)
        MP4R ABCDEF 1 
		TNT1 A 0 A_JumpIfInventory("HasUnloaded_MP40", 1, 2)
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-4)
        MP4R GGGGGGGGGGGGGGGFEDDDCCBA 1
		TNT1 A 5		
        TNT1 A 0 A_PlaySound("reload",2)
        MP2R ABCDEF 1 
		TNT1 A 0 A_JumpIfInventory("HasUnloaded_MP40", 1, 2)
		TNT1 A 0 A_FireCustomMissile("EmptyClipSpawn",-5,0,8,-4)
        MP2R GGGGGGGGGGGGGGGFEDDDCCBA 1		
		MP42 F 1		
		TNT1 A 0 A_Takeinventory("HasUnloaded_MP40",1)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
	InsertBulletsDual:
		TNT1 A 0 A_JumpIfInventory("D4MP40AmmoDual",64,"WReady")
		TNT1 A 0 A_JumpIfInventory("Clip1",1,1)
		Goto WReady
		TNT1 A 0
		TNT1 A 0 A_Giveinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Giveinventory("D4MP40AmmoDual",1)
		TNT1 A 0 A_Takeinventory("Clip1",1)
		Goto InsertBulletsDual			
	
 	 UnloadDual:
		TNT1 A 0 A_JumpIfInventory("D4MP40AmmoDual",1,1)
		Goto NoAmmo
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory ("Pumping", 1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_PlaySound("reload")
		TNT1 A 0 A_Giveinventory("HasUnloaded_MP40",1)
        MP4R ABCCDDDEFG 1
		MP2R ABCCDDDEFG 1		
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
	RemoveBulletsDual:
		TNT1 A 0 A_JumpIfInventory("D4MP40AmmoDual",1,1)
		Goto WReady
		TNT1 A 0 A_Takeinventory("D4MP40Ammo",1)
		TNT1 A 0 A_Takeinventory("D4MP40AmmoDual",1)
		TNT1 A 0 A_Giveinventory("Clip1",1)
		Goto RemoveBullets		

// ------------------------------------------------------------------------------------
//
// Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------
		
	KickFlash:
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"KickFlashDual")
		MP4S A 0
		Goto RealKickFlash
		KickFlashDual:
			M42S A 0
			Goto RealKickFlash
			RealKickFlash:
				"####" EDCB 1
				"####" A 8
				"####" BCDE 1
				Goto Flash

	AirKickFlash:
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"AirKickFlashDual")
		MP4S A 0
		Goto RealAirKickFlash
		AirKickFlashDual:
			M42S A 0
			Goto RealAirKickFlash
			RealAirKickFlash:
				"####" EDCB 1
				"####" A 10
				"####" BCDE 1
				Goto Flash
		
	SlideKickStartFlash:
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"SlideKickStartFlashDual")
		MP4S A 0
		Goto RealSlideKickStartFlash
		SlideKickStartFlashDual:
			M42S A 0
			Goto RealSlideKickStartFlash
			RealSlideKickStartFlash:
				"####" EDCB 1
				"####" A 15
	SlideKickEndFlash:
		TNT1 A 0 A_JumpIfInventory("MP40DualWield",1,"SlideKickEndFlashDual")
		MP4S A 0
		Goto RealSlideKickEndFlash
	SlideKickEndFlashDual:
		M42S A 0
		Goto RealSlideKickEndFlash
			RealSlideKickEndFlash:
				"####" A 5
				"####" BCDE 1
				Goto Flash

	TauntFlash:
		MP4S EDCB 1
		MP4S A 35
		MP4S BCDE 1
		Goto Flash
		
	PunchFlash:
	GrenadeFlash:
		MP4S ECA 1
		Goto Flash
	}
}

ACTOR HasUnloaded_MP40 : Inventory { Inventory.MaxAmount 1 }
Actor MP40DualWieldEnabler : Inventory {inventory.maxamount 1}
ACtor MP40DualWield : Inventory {inventory.maxamount 1}

ACTOR D4MP40Spawner: D4WeaponPickup Replaces MP40Spawner
{
	Inventory.PickupMessage "You got the MP-40"
	States
	{
	Spawn:
		MP40 I -1
		Stop

	Pickup:
		TNT1 A 0 A_JumpIfInventory("MP40DualWieldEnabler", 1, "GiveAmmo")
		TNT1 A 0 A_JumpIfInventory("D4MP40", 1, "DualWield")
		TNT1 A 0 A_GiveInventory("D4MP40", 1)
		Goto GiveAmmo
	DualWield:
		TNT1 A 0 A_GiveInventory("MP40DualWieldEnabler", 1)
		Goto GiveAmmo
		
	GiveAmmo:
		TNT1 A 0 A_GiveInventory("Clip1", 32)
		Stop
    }
}

ACTOR D4MP40NoAmmo: D4MP40Spawner
{
    States
    {
	Pickup:
	    TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("MP40DualWieldEnabler", 1, "GiveAmmo")
		TNT1 A 0 A_JumpIfInventory("D4MP40", 1, "DualWield")
		TNT1 A 0 A_GiveInventory("D4MP40", 1)
	GiveAmmo:
		TNT1 A 0
		Stop
    }
}

ACTOR D4MP40Ammo : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 32
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 32
   Inventory.Icon "MP40I0"
   Tag "9mm Magazine"
}

ACTOR D4MP40AmmoDual : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 64
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 64
   Inventory.Icon "MP40J0"
   Tag "Dual 9mm Magazines"   
}