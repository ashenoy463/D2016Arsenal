ACTOR D4BFG : D4WeaponBase replaces BIG_FUCKING_GUN
{
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 0
	Weapon.AmmoType1 "D4BFGAmmo"
	Inventory.PickupMessage "You Found a BFG 9000! OH YEAH!"
    Inventory.PickupSound "BFG/Select"
	Obituary "%o felt the power of argent energy at %k's fingertips."
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Tag "BFG 9000"
	States
	{
	Spawn:
		D4W7 A -1
		Stop
		
	WReady:
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickLeft")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		BF4G A 1 A_WeaponReady
		Loop

	Deselect:
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		BF4S ABCDE 1
		BF4S E 0
		Goto DeselectSequence
		
	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("BFGSelected", 1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		BF4S E 0
		Goto SelectSequence

	Ready:
		TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlaySound("BFG/Select")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		BF4S EDCBA 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	Fire:
		TNT1 A 0 A_JumpIfInventory("D4BFGAmmo",1,1)
        Goto NoAmmo	
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound("BFG/Charge", 6)
		TNT1 A 0 A_GunFlash("BFGFireFlash") // This state are already Defined in Weapon Base for Mastermind Glory Kill
		TNT1 A 0 Radius_Quake(1,4,0,2,0)
		BF4A AA 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(1,4,0,2,0)
		BF4A BB 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(1,4,0,2,0)
		BF4B AA 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(1,4,0,2,0)
		BF4B BB 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(1,4,0,2,0)
		BF4B CC 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(1,4,0,2,0)
		BF4B DD 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(2,4,0,2,0)
		BF4B EE 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(2,4,0,2,0)
		BF4B FF 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(2,4,0,2,0)
		BF4B GG 1 Bright A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(2,4,0,2,0)
		BF4B HI 1 BRIGHT A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(3,4,0,2,0)
		BF4B JK 1 BRIGHT A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(3,4,0,2,0)
		BF4B L 1 BRIGHT Offset(2,37) A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		TNT1 A 0 Radius_Quake(3,6,0,2,0)
		BF4B L 1 BRIGHT Offset(4,42) A_FireCustomMissile("GreenFlareSpawn", 0, 0, 6, -3, 0, 0)
		Goto FireBFGBall
		
	FireBFGBall:
		TNT1 A 0 Radius_Quake (4, 6, 0, 10, 0)
		TNT1 A 0 A_SetBlend("Green",0.45,50)
		TNT1 A 0 A_PlaySound("BFG/Fire", 7)
		TNT1 A 0 A_Takeinventory("D4BFGAmmo",1)
		TNT1 A 0 A_Firebullets(10,0,6,80,"BfgPuff",0,120)
		TNT1 A 0 A_FireCustomMissile("D4BFGBall", 0,1,6,-3,0,0)
		TNT1 A 0 A_SetPitch(pitch-6.0)
		BF4C A 1 Bright Offset(7,47)
		BF4C A 1 Bright Offset(10,52)
		BF4C B 2 Bright 
		BF4C CC 1 Bright A_SetPitch(pitch+0.5)
		BF4C DD 1 Bright Offset(9,50) A_SetPitch(pitch+0.5)
		BF4C EE 1 Bright Offset(8,48) A_SetPitch(pitch+0.5)
		BF4C FF 1 Bright Offset(7,46) A_SetPitch(pitch+0.5)
		Goto EndFire
		
	EndFire:
		BF4D AA 1 Offset(6,44) A_SetPitch(pitch+0.5)
		BF4D BB 2 Offset(5,42) A_SetPitch(pitch+0.5)
		BF4D C 2 Offset(4,40) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BF4D C 2 Offset(3,38) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BF4D D 2 Offset(2,36) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BF4D E 2 Offset(1,32) A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		BF4D F 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
		TNT1 A 0 A_Refire
		BF4G A 10 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady

	NoAmmo:
		BF4G A 1 A_PlaySound("BEP")
		Goto WReady

// ------------------------------------------------------------------------------------
//
// Flash States, Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------

	PunchFlash:
		BF4K ACE 1
		Goto Flash
		
	KickFlash:
		BF4K ABCD 1
		BF4K E 8
		BF4K DCBA 1
		Goto Flash

	AirKickFlash:
		BF4K ABCD 1
		BF4K E 10
		BF4K DCBA 1
		Goto Flash
		
	SlideKickStartFlash:
		BF4K ABCD 1
		BF4K E 17
	SlideKickEndFlash:
		BF4K E 5
		BF4K DCBA 1
		Goto Flash

	TauntFlash:
		BF4K ABCD 1
		BF4K E 35
		BF4K DCBA 1
		Goto Flash
		
	GrenadeFlash:
		BF4S ABCDE 1
		Goto Flash
	}
}

ACTOR BfgPuff
{
	renderstyle Translucent
	alpha 0.4
	Scale 1.5
	radius 0
	height 0
	Gravity 0.01
	DamageType Desintegrate
	Decal BFGLightning
	+NOBLOCKMAP
	+NOGRAVITY
	+NOEXTREMEDEATH
	+FORCEXYBILLBOARD
	+DONTSPLASH
	-EXPLODEONWATER
	states
	{
	Spawn:
		TNT1 A 0
	Melee:
		TNT1 A 1
		stop
	}
}

Actor D4BFGBall replaces SuperBFGBall
{
	Radius 5
	Height 5
	Projectile
	Speed 40
	Damage (random(1024, 1024))
	DamageType Desintegrate
	RenderStyle Add
	Alpha 0.9
	Scale 0.7
	Decal BFGLightning
	Obituary "%o felt the power of argent energy at %k's fingertips."
	DeathSound "BFG/Explode"
	Species "Marines"
	+RANDOMIZE
	+EXTREMEDEATH
	+THRUSPECIES
	+MTHRUSPECIES
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteAlways("CheckIfDM", 0, 0, 0, 0)//Check Effects
	Spawn1:
		BFGP A 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(90,180,0,0,5,0,0,"SuperBFGExtra")
		TNT1 A 0 A_SpawnItem("BFGLightning",0,0)
		TNT1 AA 0 A_SpawnItemEx("D4BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
		BFGP B 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(90,180,0,0,5,0,0,"SuperBFGExtra")
		TNT1 A 0 A_SpawnItem("BFGLightning",0,0)
		TNT1 AA 0 A_SpawnItemEx("D4BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
		BFGP C 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(90,180,0,0,5,0,0,"SuperBFGExtra")
		TNT1 A 0 A_SpawnItem("BFGLightning",0,0)
		TNT1 AA 0 A_SpawnItemEx("D4BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
		BFGP D 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(90,180,0,0,5,0,0,"SuperBFGExtra")
		TNT1 A 0 A_SpawnItem("BFGLightning",0,0)
		TNT1 AA 0 A_SpawnItemEx("D4BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
		BFGP E 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(90,180,0,0,5,0,0,"SuperBFGExtra")
		TNT1 A 0 A_SpawnItem("BFGLightning",0,0)
		TNT1 AA 0 A_SpawnItemEx("D4BFGTrailParticle", Random(-13, 13), Random(-13, 13), Random(0, 18), Random(1, 3), 0, (0.1)*Random(-10, 10), Random(-20, 20), 128)
		Loop
	DM:
		TNT1 A 0 A_ChangeFlag("THRUSPECIES", 0)
		TNT1 A 0 A_ChangeFlag("MTHRUSPECIES", 0)
		Goto Spawn1
	Death:
		TNT1 A 0 Radius_Quake(4,34,0,12,0)
		TNT1 A 0 Radius_Quake(1,34,0,32,0)
		TNT1 A 0 A_SpawnItemEx("ExplosionSplashSpawner", 0, 0, -20)
		TNT1 A 0 A_SpawnItemEx("UnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		//TNT1 A 0 A_SpawnItem("GreenShockWave",0,0,0)
		TNT1 A 0 A_SpawnItemEx("BFGDeathParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItemEx("BFGSuperParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItem("BFGSplashFX")
		TNT1 A 0 A_Explode(580,500,0,1,5,0,0,"SuperBFGExtra")
		BFGB AAABBBCCC 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(480,500,0,1,5,0,0,"SuperBFGExtra")
		BFGB DDDEEEFFF 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		TNT1 A 0 A_Explode(380,500,0,1,5,0,0,"SuperBFGExtra")
		BFGB GGGHHHIIIJJJKKK 1 Bright A_SpawnItem("D4GreenFlare",0,0)
		Stop
	}
}

Actor D4BFGBallExecution : D4BFGBall
{
	States
	{
	Spawn:
		Goto Death
	}
}

Actor D4BFGBallDM : D4BFGBall replaces BFGBallDM
{

	Species "NOne"
	-THRUSPECIES
	-MTHRUSPECIES
}

Actor D4GreenFlare : Flare_General
{
	alpha 0.4
	scale 0.3
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Spawn2")
	Spawn1:
		LENG A 1 bright
		TNT1 A 0 A_FadeOut(0.15)
		loop
	Spawn2:
		LENG B 1 bright
		TNT1 A 0 A_FadeOut(0.15)
		loop
	}
}

Actor D4GreenFlare2 : D4GreenFlare
{
	alpha 0.4
	scale 0.6
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,"Spawn2")
	Spawn1:
		LENG A 1 bright
		TNT1 A 0 A_FadeOut(0.07)
		loop
	Spawn2:
		LENG B 1 bright
		TNT1 A 0 A_FadeOut(0.07)
		loop
	}
}

Actor BFGLightning : Flare_General
{
	alpha 1.0
	Scale 0.8
	states
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetScale(0.8*random(-1,1),0.8*random(-1,1))
		TNT1 A 0 A_Jump(128,"Spawn1","Spawn2","Spawn3","Spawn4","Spawn5","Spawn6","Spawn7","Spawn8","Spawn9","Spawn0")
		Stop
	Spawn1:
		BFGL A 1 bright
		Stop
	Spawn2:
		BFGL B 1 bright
		Stop
	Spawn3:
		BFGL C 1 bright
		Stop
	Spawn4:
		BFGL D 1 bright
		Stop
	Spawn5:
		BFGL E 1 bright
		Stop
	Spawn6:
		BFGL F 1 bright
		Stop
	Spawn7:
		BFGL G 1 bright
		Stop
	Spawn8:
		BFGL H 1 bright
		Stop
	Spawn9:
		BFGL I 1 bright
		Stop
	Spawn0:
		BFGL J 1 bright
		Stop
	}
}

Actor BFGSplashFX
{
	+NoBlockMap
	+NoGravity
	RenderStyle Add
	+FORCERADIUSDMG
	+THRUGHOST
	Alpha 0.9
	Damage 0
	Scale 2
	Radius 1
	Height 1
	DamageType "Desintegrate"
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 AAA 0 A_SpawnItemEx("D4BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
	BFGB ABCD 2 Bright A_SpawnItem("D4GreenFlare2",0,0)
	TNT1 AAA 0 A_SpawnItemEx("D4BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
	BFGB EFGH 2 Bright A_SpawnItem("D4GreenFlare2",0,0)
	TNT1 AAA 0 A_SpawnItemEx("D4BFGExtraParticle", 0, 0, 14, (0.1)*Random(10, 40), 0, (0.1)*Random(-40, 40), Random(0, 360), 128)
	BFGB IJK 2 Bright A_SpawnItem("D4GreenFlare2",0,0)
	Stop
	}
}

Actor D4BFGExtraParticle replaces BFGExtraParticle
{
	Height 0
	Radius 0
	Mass 0
	+Missile
	+NoBlockMap
	+DontSplash
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.035
	States
	{
	Spawn:
	Death:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Fade1","Fade2","Fade3")
	Fade1:
		SPKG A 1 Bright A_FadeOut(0.03)
		Loop
	Fade2:
		SPKG A 1 Bright A_FadeOut(0.05)
		Loop
	Fade3:
		SPKG A 1 Bright A_FadeOut(0.07)
		Loop
	}
}

Actor D4BFGTrailParticle replaces BFGTrailParticle
{
	Height 0
	Radius 0
	Mass 0
	Speed 3
	+Missile
	+NoBlockMap
	+NoGravity
	+DontSplash
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.05
	States
	{
	Spawn:
	Death:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Fade1","Fade2","Fade3")
	Fade1:
		SPKG A 1 Bright A_FadeOut(0.01)
		SPKG A 1 Bright A_FadeOut(0.01)
		SPKG A 1 Bright
		Loop
	Fade2:
		SPKG A 1 Bright A_FadeOut(0.01)
		Loop
	Fade3:
		SPKG A 1 Bright A_FadeOut(0.01)
		SPKG A 1 Bright A_FadeOut(0.01)
		SPKG A 1 Bright A_FadeOut(0.02)
		Loop
	}
}