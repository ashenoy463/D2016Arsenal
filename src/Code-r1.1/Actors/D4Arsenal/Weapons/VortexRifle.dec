ACTOR D4VortexRifle : D4WeaponBase replaces Railgun
{
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 8
	Weapon.AmmoType1 "D4Slugs"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "VortexCharge"
    Inventory.PickupSound "PLSDRAW"
	Inventory.PickupMessage "Picked up a Vortex Rifle! A sniper's delight!"
	Obituary "%k sniper capabilities are too much for %o."
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Tag "Vortex Rifle"
	States
	{
	WReady:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ReadyZoomed")
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"StartUpgrader")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		VRTG A 1 A_WeaponReady
		Loop

	ReadyZoomed:
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"AltFire")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"AltFire")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"AltFire")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"AltFire")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "AltFire")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "AltFire")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"AltFire")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",52,"ReadyZoomedFull")
		R016 P 1 A_WeaponReady
		Goto WReady	
		
	ReadyZoomedFull:
		R016 Q 1 A_WeaponReady
		Goto WReady
				
	StartUpgrader:
		TNT1 A 0 A_TakeInventory("StartUpgrader",1)
		TNT1 A 0 A_GiveInventory("UpgradeVortexRifle",1)
		TNT1 A 0 A_SelectWeapon("D4WeaponUpgrader")
		Goto WReady
		
	Spawn:
		D4W6 B -1
		Stop

	DeselectZoomed:
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)	
		TNT1 A 0 ACS_NamedExecuteAlways("SetPlayerSpeed",0,100,0,0)
		TNT1 A 0 A_Takeinventory("VortexCharge",32768)
		VTXG A 0 A_TakeInventory("VortexEnhancedView",32768)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)	
		TNT1 A 0 A_ZoomFactor(1.0)
		VRTG GFE 1
	Deselect:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"DeselectZoomed")
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
		VRTS EDCBA 1
		VRTS A 0
		Goto DeselectSequence

	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("RailGunSelected",1)
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		VRTS A 0
		Goto SelectSequence	

	Ready:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("Weapon/Switch")
	SelectAnimation:
		TNT1 A 0 A_Giveinventory("NoParkour",1)
		VRTS ABCDE 1
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		Goto WReady
		
	Fire:
		"####" "#" 0 A_JumpIfInventory("VortexFireToken",1,"CantFireYet")
		TNT1 A 0 ACS_NamedExecuteAlways("VortexRifleTimer",0,0,0,0)
		TNT1 A 0 A_GiveInventory("VortexFireToken",1)
		TNT1 A 0 A_PlaySound("Vortex/Fire",CHAN_WEAPON,1.0,false,0.9)
		TNT1 A 0 A_JumpIfInventory("Zoomed", 1, "FireZoomed")
		TNT1 A 0 A_GunFlash("PrimaryFire")
		TNT1 A 0 A_Giveinventory("NoParkour",1)
		VRTF ABC 1 BRIGHT A_SetPitch(-2.0 + pitch)
		VRTG BCDEAA 1 A_SetPitch(1.0 + pitch)
		VRTG A 10
		VRTG A 6 A_WeaponReady(WRF_NOBOB)
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		Goto WReady

	FireZoomed:
		TNT1 A 0 A_GunFlash("PrimaryZoomed")
		R016 PP 1 BRIGHT A_SetPitch(-2.0 + pitch)
		R016 P 10
		R016 P 2 A_SetPitch(1.0 + pitch)
		R016 PP 3 A_SetPitch(1.0 + pitch)
		TNT1 A 0 A_Refire
		R016 P 6 A_WeaponReady(WRF_NOBOB)
		Goto ReadyZoomed

	CantFireYet:
		"####" "#" 2 
		Goto WReady

	AltFire:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("VortexFireToken",1,"DontPlayChargeSound") //if wep not ready to refire, will only play zoomin sound
		Goto ZoomIn
		
	ZoomIn:
		TNT1 A 0 A_JumpIfInventory("D4Slugs",1,"PlayChargeSound")
		Goto DontPlayChargeSound
		 
		DontPlayChargeSound:
			TNT1 A 0 A_PlaySound("Repeater/Zoom/In",5,1)
			Goto ResumeZoomIn
		PlayChargeSound:
			TNT1 A 0 A_PlaySound("Vortex/ZoomNLoop",5,1,1)
			Goto ResumeZoomIn
		
	ResumeZoomIn:
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_Giveinventory("ADSmode",1)
		TNT1 A 0 A_ZoomFactor(2.5)
		TNT1 A 0 A_Giveinventory("NoParkour",1)
		TNT1 A 0 A_TakeInventory("VortexCharge", 100)
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleScope",1,"BetterZoomIn")
		TNT1 A 0 ACS_NamedExecuteAlways("SetPlayerSpeed",0,50,0,0)
		VRTG EFG 1
		Goto ReadyZoomed
		
		BetterZoomIn:
			TNT1 A 0 A_Giveinventory("VortexEnhancedView",1)
			VRTG EFG 1
			Goto ReadyZoomed
		
	ZoomOut:
		TNT1 A 0 A_GunFlash
		VTXG A 0 A_StopSound(5)
		TNT1 A 0 ACS_NamedExecuteAlways("SetPlayerSpeed",0,100,0,0)
		TNT1 A 0 A_Takeinventory("VortexCharge",32768)
		VTXG A 0 A_TakeInventory("VortexEnhancedView",32768)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_ZoomFactor(1.0)
		VRTG GFE 1
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		Goto WReady	
		
// ------------------------------------------------------------------------------------
//
// Fire Common and Charge Function
//
// ------------------------------------------------------------------------------------
		
	Flash:
		TNT1 A 1 A_JumpIfInventory("Zoomed",1,"ChargeScope")
		Goto Flash
		
	ChargeScope:
		TNT1 A 0 A_JumpIfInventory("VortexCharge",52,"FullCharged")
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleDamage", 2, "ChargeUpgrade2")
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleDamage", 1, "ChargeUpgrade")
		TNT1 A 1 A_JumpIfInventory("Zoomed",1,"ChargeNoUpgrade")
		Goto Flash
		
	ChargeNoUpgrade:
		TNT1 A 5 A_JumpIfInventory("VortexCharge",52,"FullCharged")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"GiveToken")
		Goto Flash
		
	ChargeUpgrade:
		TNT1 A 4 A_JumpIfInventory("VortexCharge",52,"FullCharged")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"GiveToken")
		Goto Flash
		
	ChargeUpgrade2:
		TNT1 A 3 A_JumpIfInventory("VortexCharge",52,"FullCharged")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"GiveToken")
		Goto Flash

	GiveToken:
		TNT1 A 0 A_JumpIfInventory("D4Slugs",1,2)
		TNT1 A 0 A_StopSound(5)
		Goto ChargeScope
		TNT1 A 0 A_GiveInventory("VortexCharge", 1)
		Goto ChargeScope
		
	FullCharged:
		TNT1 A 0 A_PlaySound("Vortex/Charged",5,0.6)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,1)
		Goto Flash
		TNT1 A 0 A_JumpIfInventory("VortexCharge",52,1)
		Goto ChargeScope
		TNT1 A 1
		Goto FullCharged+1
		
	PrimaryFire:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetBlend("62 42 F4",0.07,6)
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleDamage", 2, "Upgrade2Fire")
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleDamage", 1, "Upgrade1Fire")
		TNT1 A 0 A_FireBullets(0,0,1,0,"VortexImpact",0)
		TNT1 A 0 A_RailAttack(0,2,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"none",0,0,0,50,1.0,-0.5,"none",-5)
		TNT1 A 0 A_FireBullets(0,0,1,50,"VortexPuff",0)
		Goto Flash
		
		Upgrade1Fire:
			TNT1 A 0 A_FireBullets(0,0,1,0,"VortexImpactUpgraded",0)
			TNT1 A 0 A_RailAttack(0,2,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"none",0,0,0,50,1.0,-3,"none",-5)
			TNT1 A 0 A_FireBullets(0,0,1,70,"VortexPuffUpgraded",0)
			Goto Flash		
		Upgrade2Fire:
			TNT1 A 0 A_FireBullets(0,0,1,0,"VortexImpactFullUpgraded",0)
			TNT1 A 0 A_RailAttack(90,2,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5)
			TNT1 A 0 A_FireBullets(0,0,1,90,"VortexPuffFullUpgraded",0)
			Goto Flash
		
	PrimaryZoomed:
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleDamage", 2, "FullyUpgraded")
		TNT1 A 0 A_RailAttack(0,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"none",0,0,0,50,1.0,-0.5,"none",-5)
		TNT1 A 0 A_JumpIfInventory("HasUpgradeVortexRifleDamage", 1, "CheckForUpgraded")
		TNT1 A 0 A_FireBullets(0,0,1,0,"VortexImpact",0)
		TNT1 A 0 A_JumpIfInventory("VortexCharge",52,"FireLVL6")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",44,"FireLVL5")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",36,"FireLVL4")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",27,"FireLVL3")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",18,"FireLVL2")
		TNT1 A 0 A_JumpIfInventory("VortexCharge", 9,"FireLVL1")
		TNT1 A 0 A_FireBullets(0,0,1,60,"VortexPuff",0) 
		Goto FinishZoomed
			
		FireLVL6: 
			TNT1 A 0 A_FireBullets(0,0,1,300,"VortexPuff",0) 
			Goto FinishZoomed
		FireLVL5: 
			TNT1 A 0 A_FireBullets(0,0,1,260,"VortexPuff",0) 
			Goto FinishZoomed
		FireLVL4: 
			TNT1 A 0 A_FireBullets(0,0,1,220,"VortexPuff",0) 
			Goto FinishZoomed
		FireLVL3: 
			TNT1 A 0 A_FireBullets(0,0,1,180,"VortexPuff",0) 
			Goto FinishZoomed
		FireLVL2: 
			TNT1 A 0 A_FireBullets(0,0,1,140,"VortexPuff",0) 
			Goto FinishZoomed
		FireLVL1: 
			TNT1 A 0 A_FireBullets(0,0,1,100,"VortexPuff",0) 
			Goto FinishZoomed
	
	CheckForUpgraded:
		TNT1 A 0 A_FireBullets(0,0,1,0,"VortexImpactUpgraded",0)
		TNT1 A 0 A_JumpIfInventory("VortexCharge",52,"FireLVL6Upgraded")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",44,"FireLVL5Upgraded")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",36,"FireLVL4Upgraded")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",27,"FireLVL3Upgraded")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",18,"FireLVL2Upgraded")
		TNT1 A 0 A_JumpIfInventory("VortexCharge", 9,"FireLVL1Upgraded")
		TNT1 A 0 A_FireBullets(0,0,1,80,"VortexPuffUpgraded",0) 
		Goto FinishZoomed
		
		FireLVL6Upgraded: 
			TNT1 A 0 A_FireBullets(0,0,1,320,"VortexPuffUpgraded",0) 
			Goto FinishZoomed
		FireLVL5Upgraded: 
			TNT1 A 0 A_FireBullets(0,0,1,280,"VortexPuffUpgraded",0) 
			Goto FinishZoomed
		FireLVL4Upgraded: 
			TNT1 A 0 A_FireBullets(0,0,1,250,"VortexPuffUpgraded",0) 
			Goto FinishZoomed
		FireLVL3Upgraded: 
			TNT1 A 0 A_FireBullets(0,0,1,210,"VortexPuffUpgraded",0) 
			Goto FinishZoomed
		FireLVL2Upgraded: 
			TNT1 A 0 A_FireBullets(0,0,1,170,"VortexPuffUpgraded",0) 
			Goto FinishZoomed
		FireLVL1Upgraded: 
			TNT1 A 0 A_FireBullets(0,0,1,130,"VortexPuffUpgraded",0) 
			Goto FinishZoomed

	FullyUpgraded:
		TNT1 A 0 A_FireBullets(0,0,1,0,"VortexImpactFullUpgraded",0)
		TNT1 A 0 A_JumpIfInventory("VortexCharge",52,"FireLVL6Full")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",44,"FireLVL5Full")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",36,"FireLVL4Full")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",27,"FireLVL3Full")
		TNT1 A 0 A_JumpIfInventory("VortexCharge",18,"FireLVL2Full")
		TNT1 A 0 A_JumpIfInventory("VortexCharge", 9,"FireLVL1Full")
		TNT1 A 0 A_RailAttack(100,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
		Goto FinishFullUpgrade
		
		FireLVL6Full: 
			TNT1 A 0 A_RailAttack(400,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
			Goto FinishFullUpgrade
		FireLVL5Full: 
			TNT1 A 0 A_RailAttack(350,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
			Goto FinishFullUpgrade
		FireLVL4Full: 
			TNT1 A 0 A_RailAttack(300,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
			Goto FinishFullUpgrade
		FireLVL3Full: 
			TNT1 A 0 A_RailAttack(250,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
			Goto FinishFullUpgrade
		FireLVL2Full: 
			TNT1 A 0 A_RailAttack(200,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
			Goto FinishFullUpgrade
		FireLVL1Full: 
			TNT1 A 0 A_RailAttack(150,0,1,"7B00FF","none",RGF_SILENT|RGF_FULLBRIGHT,0,"VortexPuffFullUpgraded",0,0,0,50,1.0,-3,"none",-5) 
			Goto FinishFullUpgrade
		
			FinishFullUpgrade:
				TNT1 A 0 A_FireBullets(0,0,1,90,"VortexPuffFullUpgraded",0)
				Goto FinishZoomed

	FinishZoomed:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_TakeInventory("VortexCharge",32768)
		TNT1 A 0 A_SetBlend("62 42 F4",0.12,12)
		TNT1 A 0 A_ZoomFactor(2.4)
		TNT1 A 2
		TNT1 A 1
		TNT1 A 0 A_ZoomFactor(2.5)
		TNT1 A 0 A_JumpIfInventory("D4Slugs",1,"WaitDuringCooldown")
		TNT1 A 0 A_StopSound(5)
		Goto Flash

	WaitDuringCooldown:
		TNT1 A 1
		TNT1 A 0 A_JumpIfInventory("VortexFireToken",1,"WaitDuringCooldown")
		TNT1 A 0 A_PlaySound("Vortex/ZoomNLoop",5,1,1)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ChargeScope")
		Goto Flash
		
// ------------------------------------------------------------------------------------
//
// Flash States, Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------
		
	PunchFlash:
		VRTK ACE 1
		Stop
		
	KickFlash:
		VRTK ABCD 1
		VRTK E 8
		VRTK DCBA 1
		Goto Flash

	AirKickFlash:
		VRTK ABCD 1
		VRTK E 10
		VRTK DCBA 1
		Goto Flash
		
	SlideKickStartFlash:
		VRTS EDCB 1
		VRTS A 17
	SlideKickEndFlash:
		VRTS A 5
		VRTS BCDE 1
		Goto Flash

	TauntFlash:
		VRTK ABCD 1
		VRTK E 35
		VRTK DCBA 1
		Goto Flash
		
	GrenadeFlash:
		VRTK ABCDE 1
		Stop
	}
}

Actor VortexEnhancedView : PowerLightAmp { Powerup.Duration -1000 }
Actor VortexFireToken : Inventory { Inventory.MaxAmount 1 }

Actor VortexPuff : Hitpuff
{
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

Actor VortexPuffUpgraded : VortexPuff
{
	DamageType Shotgun
}

Actor VortexPuffFullUpgraded : VortexPuff
{
	DamageType Railgun
	Radius 2
	Height 2
}

Actor VortexImpact
{
  Radius 1
  Height 1
  Damage 0
  +NoBlockMap
  +NoGravity
  +ActivateImpact
  +ActivatePCross
  +NoTeleport
  +AlwaysPuff
  +PuffGetsOwner
  +ForceRadiusDmg
  +NOINTERACTION
  +PUFFONACTORS
  Renderstyle Add
  Alpha 0.75
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
	TNT1 A 1 A_PlaySound("Vortex/Impact",CHAN_AUTO,1.0,false,1.5)
	TNT1 A 0 A_SpawnItem("BlueFlare")
	TNT1 A 0 A_SpawnItem("WhiteShockwaveSmall")
	Stop
  }
}

Actor VortexImpactUpgraded : VortexImpact
{
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
	TNT1 A 1 A_PlaySound("Vortex/Impact",CHAN_AUTO,1.0,false,1.5)
	TNT1 AAA 0 A_SpawnItem("BlueFlare")
	TNT1 AAA 0 A_SpawnItem("WhiteShockwaveSmall")
	Stop
  }
}

Actor VortexImpactFullUpgraded : VortexImpact
{
  States
  {
  Spawn:	//hit everything else
	TNT1 A 0
	TNT1 A 0 A_SpawnItemEx ("RicoChet",0,0,-5,0,0,0,0,SXF_NOCHECKPOSITION,0)
	TNT1 A 1 A_PlaySound("Vortex/Impact",CHAN_AUTO,1.0,false,1.5)
	TNT1 AAA 0 A_SpawnItem("BlueFlare")
	TNT1 AAA 0 A_SpawnItem("GreenFlareMedium",0,0)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 0, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 45, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 90, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 135, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 180, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 225, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 270, 2)
	TNT1 A 0 A_CustomMissile ("BluePlasmaFireNonStatic", 2, 0, 315, 2)
	TNT1 AAA 0 A_SpawnItem("WhiteShockwaveBig")
	TNT1 AAA 0 Bright A_SpawnItem("GreenShockWave",0,0,0)
	Stop
  }
}

ACTOR VortexCharge : Ammo
{
	Inventory.Amount 0
	Inventory.MaxAmount 52
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 52
	Inventory.Icon "D4W6B0"
	Tag "Vortex Charge"
}
