ACTOR D4GaussCannon : D4WeaponBase replaces BFG10k
{
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 6
	Weapon.AmmoType1 "D4Slugs"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "PrecisionCount1Second"
	tag "Gauss Cannon"
    Inventory.PickupSound "Gauss/Select"
	Obituary "%o was blasted by %k's Gauss Cannon."
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	States
	{	
	Spawn:
		D4W7 B -1
		Stop

	WReady:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ReadyZoomed")
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"SwapMod")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"StartUpgrader")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"ReadySiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"ReadyPrecision")
		GA1G A 1 A_WeaponReady
		Loop
		
		ReadyPrecision:
			GA2G A 1 A_WeaponReady
			Goto WReady
		ReadySiege:
			TNT1 A 0 A_JumpIfInventory("SiegeAltMode", 1, "ReadySiegeAlt")
			GA3G A 1 A_WeaponReady
			Goto WReady
		ReadySiegeAlt:
			G3SG ABCD 1 BRIGHT A_WeaponReady(WRF_NOBOB|WRF_NOSWITCH)
			Goto ReadySiege
		
	ReadyZoomed:
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "ZoomOut")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "ZoomOut")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"ZoomOut")
		R016 N 1 BRIGHT A_WeaponReady(WRF_NOBOB)
		Goto WReady
	
	Deselect:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,2)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto ProccedDeselect
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_playsound("Gauss/Zoom/Decharge", CHAN_WEAPON)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_TakeInventory("PrecisionCount1Second",32767)
		TNT1 A 0 A_Takeinventory("GaussReadyZoomed",1)
        TNT1 A 0 A_ZoomFactor(1.0)
		// Continue to Deselect
	ProccedDeselect:
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"DeselectSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"DeselectPrecision")
		GA1S A 0
		Goto RealDeselect
		
		DeselectPrecision:
			GA2S A 0
			Goto RealDeselect
		DeselectSiege:
			GA3S A 0
			Goto RealDeselect
		
	RealDeselect:
		"####" EDCBA 1
		"####" A 0
		Goto DeselectSequence
		
	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("BFG10kSelected",1)
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"StartSelectSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"StartSelectPrecision")
		GA1S A 0
		Goto SelectSequence
		StartSelectPrecision:
			GA2S A 0
			Goto SelectSequence
		StartSelectSiege:
			GA3S A 0
			Goto SelectSequence
		
	Ready:
		TNT1 A 0 A_GunFlash
        TNT1 A 0 A_PlaySound("Gauss/Select")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"SelectSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"SelectPrecision")
		GA1S A 0
		Goto RealSelect
		
		SelectPrecision:
			GA2S A 0
			Goto RealSelect
		SelectSiege:
			GA3S A 0
			Goto RealSelect
		
	RealSelect:
		"####" ABCDE 1
		TNT1 A 0 A_PlaySound("Gauss/Idle", 5, 1, TRUE)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
// ------------------------------------------------------------------------------------
//
// Fire
//
// ------------------------------------------------------------------------------------
		
	Fire:
        "####" "#" 0 A_GiveInventory("NoParkour", 1)
		"####" "#" 0 A_JumpIfInventory("D4Slugs",2,1)
        Goto NoAmmo
		"####" "#" 0 A_AlertMonsters
        "####" "#" 0 A_JumpIfInventory("SiegeAltMode", 1, "SiegeFire")
        "####" "#" 0 A_JumpIfInventory("Zoomed", 1, "FireZoomed")
		TNT1 A 0 A_GunFlash("FirePrimary")
		TNT1 A 0 A_jumpIfInventory("ModSiegeModeActive", 1, "FireSiege")
		TNT1 A 0 A_jumpIfInventory("ModPrecisionScopeActive", 1, "FirePrecision")
		GA1F A 0
		Goto FireRelease
		
		FirePrecision:
			GA2F A 0
			Goto FireRelease
		FireSiege:
			GA3F A 0
			Goto FireRelease
		
	FireRelease:
        "####" ABCD 1 BRIGHT
		"####" E 6
		"####" A 0 A_playsound("Gauss/Load1", 5)
		"####" FGHIJ 2
		"####" KLMNO 1
		Goto FinishFire
		
	FinishFire:
		TNT1 A 0 A_jumpIfInventory("ModSiegeModeActive", 1, "FinishFireSiege")
		TNT1 A 0 A_jumpIfInventory("ModPrecisionScopeActive", 1, "FinishFirePrecision")
		GA1G A 0
		Goto FinishFireRelease
		
		FinishFirePrecision:
			GA2G A 0
			Goto FinishFireRelease
		FinishFireSiege:
			GA3G A 0
			Goto FinishFireRelease

	FinishFireRelease:
		"####" AAAAAAAAAA 1
        TNT1 A 0 A_Refire("Fire")
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FirePrimary:
		//TNT1 A 0 A_AlertMonsters
		TNT1 A 1 A_FireCustomMissile("BlueFlareSpawn",-5,0,0,0)
		TNT1 A 0 A_PlaySound ("Gauss/Fire",7)
		TNT1 A 0 A_FireBullets(0,0,1,0,"GaussCannonExplosion")
		RAIL A 0 A_RailAttack(Random(110,100), 0, 0, "none", "none", RGF_FULLBRIGHT, 1, "RailgunPuff2")
		TNT1 A 0 A_RailAttack(0, 10, 0, "0F 0 D3", "53 53 53", RGF_NOPIERCING|RGF_SILENT|RGF_FULLBRIGHT, 1, "none",0,0,8192,0,16,0,"GaussCannonParticle",-3)
		TNT1 A 0 A_TakeInventory("D4Slugs",2)
		
		// Recoil
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*2,0,sin(pitch)*2,CVF_RELATIVE) //this recoil applies always
		TNT1 A 0 A_CheckFloor(2)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*12,0,sin(pitch)*12,CVF_RELATIVE) //only when in air
			
		TNT1 A 1 A_FireCUstomMissile("ShakeYourAssQuad")
		TNT1 A 0 A_SetPitch(Pitch-3)
		TNT1 A 1 A_FireCUstomMissile("ShakeYourAssQuad")
		TNT1 A 1 A_FireCUstomMissile("ShakeYourAssQuad")
		TNT1 A 0 A_FireCUstomMissile("ShakeYourAssQuad")
		TNT1 AAA 1 A_SetPitch(Pitch+1)
		Goto Flash
		
	NoAmmo:
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"NoAmmoSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"NoAmmoPrecision")
		GA1G A 0 
		Goto NoAmmoChecked
		
		NoAmmoPrecision:
			GA2G A 0
			Goto NoAmmoChecked	
		NoAmmoSiege:
			GA3G A 0
			Goto NoAmmoChecked
		
	NoAmmoChecked:
		"####" "#" 1 A_playsound("BEPBEP")
		Goto WReady
		
// ------------------------------------------------------------------------------------
//
// Alt Fire
//
// ------------------------------------------------------------------------------------
		
	AltFire:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive", 1, "ZoomIn")
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive", 1, "StartSiege")
		Goto NoUpgrades
		
// ------------------------------------------------------------------------------------
//
// Precision Scope
//
// ------------------------------------------------------------------------------------
		
	Flash:
		TNT1 A 1
        TNT1 A 0 A_JumpIfInventory("SiegeAltMode", 1, 2)
		TNT1 A 0 A_JumpIfInventory("Zoomed", 1, "ZoomFlash")
		Loop
		TNT1 A 0 A_TakeInventory("ExecuteDownedEnemy",1)
		TNT1 A 0 A_TakeInventory("Kicking",1)
		TNT1 A 0 A_TakeInventory("Fistting",1)
		TNT1 A 0 A_TakeInventory("Taunting",1)
		TNT1 A 0 A_TakeInventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("Salute1",1)
		TNT1 A 0 A_TakeInventory("Salute2",1)
		TNT1 A 0 A_TakeInventory("TossGrenade",1)
		Loop
		
	ZoomIn:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ZoomOut")
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_Giveinventory("ADSmode",1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
        TNT1 A 0 A_ZoomFactor(2.0)
        Goto WReady
		
	ZoomOut:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_playsound("Gauss/Zoom/Decharge", 7)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		TNT1 A 0 A_TakeInventory("PrecisionCount1Second",32767)
		TNT1 A 0 A_Takeinventory("GaussReadyZoomed",1)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
        TNT1 A 0 A_ZoomFactor(1.0)
        Goto WReady
		
	FireZoomed:
		R016 N 0 A_JumpIfInventory("D4Slugs",3,1)
		Goto NoAmmoChecked
		TNT1 A 0 A_GunFlash("PrecisionFire")
		R016 N 22 BRIGHT
		Goto WReady
		
	PrecisionFire:			
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_PlaySound ("Gauss/Zoom/Fire",7)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("D4Slugs", 3,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second",60,"PrecisionLVL6")
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second",50,"PrecisionLVL5")
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second",40,"PrecisionLVL4")
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second",30,"PrecisionLVL3")
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second",20,"PrecisionLVL2")
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second",10,"PrecisionLVL1")
		RAIL A 0 A_RailAttack(100,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
		Goto PrecisionEnd
		
			PrecisionLVL6:	//768 + ~128 = ~896
				TNT1 A 0 A_RailAttack(1000,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
				Goto PrecisionEnd
			PrecisionLVL5: //640 + ~128 = ~768
				TNT1 A 0 A_RailAttack(768,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
				Goto PrecisionEnd
			PrecisionLVL4: //512 + ~128 = ~640
				TNT1 A 0 A_RailAttack(512,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
				Goto PrecisionEnd
			PrecisionLVL3: //384 + ~128 = ~512
				TNT1 A 0 A_RailAttack(384,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
				Goto PrecisionEnd
			PrecisionLVL2: //256 + ~128 = ~384
				TNT1 A 0 A_RailAttack(256,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
				Goto PrecisionEnd
			PrecisionLVL1:	//128 + ~128  = ~256
				TNT1 A 0 A_RailAttack(128,0,0,"none","none",RGF_SILENT|RGF_FULLBRIGHT,1,"RailgunPuff2")
				Goto PrecisionEnd
		
	PrecisionEnd:
		TNT1 A 0 A_RailAttack(0,0,0,"0F 0 D3","53 53 53",RGF_NOPIERCING|RGF_SILENT|RGF_FULLBRIGHT,0,"none",0,0,8192,0,16,0,"GaussCannonParticle",0)
		TNT1 A 0 A_FireBullets(0,0,1,0,"GaussCannonExplosion")
		TNT1 A 0 A_TakeInventory("PrecisionCount1Second",32767)
		TNT1 A 1 A_SetPitch(Pitch-3)
		TNT1 A 0 A_TakeInventory("GaussReadyZoomed",1)
		// Recoil
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*2,0,sin(pitch)*2,CVF_RELATIVE) //this recoil applies always
		TNT1 A 0 A_CheckFloor(2)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*12,0,sin(pitch)*12,CVF_RELATIVE) //only when in air
		TNT1 AA 1 A_SetPitch(Pitch-3.5)
		TNT1 A 0 A_FireCUstomMissile("ShakeYourAssQuad")
		TNT1 AAAAAAAAAA 1 A_SetPitch(Pitch+1)
		TNT1 A 0 A_JumpIfInventory("D4Slugs",3,"ZoomFlash")
		Goto Flash
		
	ZoomFlash:
		TNT1 A 0 A_PlaySound ("Gauss/Zoom/Charge",CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("D4Slugs",3,"ZoomFlashGiveFirstCharge")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto ZoomFlashContinue
	ZoomFlashGiveFirstCharge:
		TNT1 A 0 A_GiveInventory("PrecisionCount1Second", 1)
	ZoomFlashContinue:
		TNT1 A 7
		TNT1 A 0 A_JumpIfInventory("D4Slugs",3,"ZoomFlashLoopSound")
		Goto ZoomFlashLoop
		
	ZoomFlashLoopSound:
		TNT1 A 0 A_playsound("Gauss/Zoom/Loop", CHAN_WEAPON, 1, TRUE)
		Goto ZoomFlashLoop

	ZoomFlashLoop:
		TNT1 A 0 A_JumpifInventory("Zoomed", 1,1)
		Goto Flash
		TNT1 A 2
		TNT1 A 0 A_JumpIfInventory("D4Slugs",3,1)
		Goto ZoomFlashLoop
		TNT1 A 0 A_JumpIfInventory("PrecisionCount1Second", 60, "ZoomFlashReady")
		TNT1 A 0 A_GiveInventory("PrecisionCount1Second", 1)
		Loop
		
	ZoomFlashReady:
		TNT1 A 0 A_Giveinventory("GaussReadyZoomed",1)// Used for HUDs to indicate the powerfull fire are ready
		TNT1 A 1
		TNT1 A 0 A_JumpifInventory("Zoomed", 1, "ZoomFlashReady")
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		Goto Flash
		
// ------------------------------------------------------------------------------------
//
// Siege Mode
//
// ------------------------------------------------------------------------------------
		
	SiegeFire:
		TNT1 A 0 A_stopsound(CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("D4Slugs",8,1)
        Goto UnSiege
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireBullets(0,0,1,0,"GaussCannonExplosionSIEGE2")
		TNT1 A 0 A_FireCustomMissile("GaussCannonExplosionSIEGE", 0,0,0,0)
		TNT1 A 0 A_FireCustomMissile("RipperSIEGE",0,0,0,0)
		RAIL A 0 A_RailAttack(300, 0, 0, "none", "none", RGF_FULLBRIGHT, 1, "RailgunPuff2")
		TNT1 A 0 A_RailAttack(0, -1, 0, "0F 0 D3", "53 53 53", RGF_SILENT|RGF_FULLBRIGHT,0,"none",0,0,8192,0,32,0,"GaussCannonChargedParticle",0)
		//TNT1 A 0 A_RailAttack(300, -1, 0, "0F 0 D3", "53 53 53", RGF_SILENT|RGF_FULLBRIGHT,0,"GaussCannonExplosionSIEGE2",0,0,8192,0,32,0,"GaussCannonChargedParticle",0)
		TNT1 A 0 A_RailAttack(100, -1, 0, "0F 0 D3", "53 53 53", RGF_SILENT|RGF_FULLBRIGHT,0,"RailgunPuff2",0,0,8192,0,24,0,"VNewTrail",16)
		TNT1 A 0 A_RailAttack(100, -1, 0, "0F 0 D3", "53 53 53", RGF_SILENT|RGF_FULLBRIGHT,0,"RailgunPuff2",0,0,8192,0,16,0,"VNewTrail",-16)
		TNT1 A 0 A_RailAttack(100, -1, 0, "0F 0 D3", "53 53 53", RGF_SILENT|RGF_FULLBRIGHT,0,"RailgunPuff2",0,0,8192,0,8,0,"VNewTrail", -3)
		TNT1 A 0 A_RailAttack(100, -1, 0, "0F 0 D3", "53 53 53", RGF_SILENT|RGF_FULLBRIGHT,0,"RailgunPuff2",0,0,8192,0,4,0,"VNewTrail", -3)
		TNT1 A 0 A_PlaySound ("Gauss/SIEGE/Fire",7)
		TNT1 A 0 A_TakeInventory("D4Slugs", 8,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_Takeinventory("SiegeAltMode",1)
		G3SF A 1 BRIGHT A_ZoomFactor(0.83)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*2,0,sin(pitch)*2,CVF_RELATIVE) //this recoil applies always
		G3SF B 1 BRIGHT A_ZoomFactor(0.84)
		TNT1 A 0 A_CheckFloor(2)
		TNT1 A 0 A_ChangeVelocity(-cos(-pitch)*12,0,sin(pitch)*12,CVF_RELATIVE) //only when in air
		G3SF C 1 BRIGHT A_ZoomFactor(0.85)
		G3SF D 1 BRIGHT A_ZoomFactor(0.86)
		G3SR A 2 A_ZoomFactor(0.87)
		G3SR B 2 A_ZoomFactor(0.88)
		G3SR C 2 A_ZoomFactor(0.89)
		G3SR D 2 A_ZoomFactor(0.90)
		TNT1 A 0 A_playsound("Gauss/Siege/STAP", 6)
		G3SR E 2 A_ZoomFactor(0.91)
		G3SR F 2 A_ZoomFactor(0.92)
		G3SR G 2 A_ZoomFactor(0.93)
		G3SR H 2 A_ZoomFactor(0.94)
		G3SR I 2 A_ZoomFactor(0.95)
		G3SR J 2 A_ZoomFactor(0.96)
		G3SR K 2 A_ZoomFactor(0.97)
		G3SR L 2 A_ZoomFactor(0.98)
		G3SR M 2 A_ZoomFactor(0.99)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 ACS_NamedExecuteAlways("SetPlayerSpeed",0,100,0,0)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	StartSiege:
		TNT1 A 0 A_JumpIfInventory("SiegeAltMode",1,"UnSiege")
		TNT1 A 0 ACS_NamedExecuteAlways("SetPlayerSpeed",0,30,0,0)
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_Giveinventory("SiegeAltMode",1)
		TNT1 A 0 A_playsound("Gauss/Siege/Alt1", 6)
		TNT1 A 0 A_playsound("Gauss/Siege/Loop", CHAN_WEAPON,1,TRUE)
        GA3M A 2 A_ZoomFactor(0.99)
		GA3M B 2 A_ZoomFactor(0.98)
		GA3M C 2 A_ZoomFactor(0.97)
		GA3M D 2 A_ZoomFactor(0.96)
		GA3M E 2 A_ZoomFactor(0.95)
		GA3M F 2 A_ZoomFactor(0.94)
		GA3M G 2 A_ZoomFactor(0.93)
		GA3M H 2 A_ZoomFactor(0.92)
		GA3M I 2 A_ZoomFactor(0.91)
		GA3M J 2 A_ZoomFactor(0.9)
		GA3M K 2
		GA3M L 2
		GA3M M 2
		TNT1 A 0 A_PlaySound("Gauss/Siege/Alt2", 6)
        Goto ReadySiegeAlt
		
	UnSiege:
		TNT1 A 0
		TNT1 A 0 A_stopsound(CHAN_WEAPON)
		TNT1 A 0 A_Takeinventory("SiegeAltMode",1)
		GA3M M 1 
		GA3M L 1 
		GA3M K 1 
		GA3M J 1 A_ZoomFactor(0.9)
		TNT1 A 0 A_playsound("Gauss/Siege/STAP", 7)
		GA3M I 1 A_ZoomFactor(0.91)
		GA3M H 1 A_ZoomFactor(0.92)
		GA3M G 1 A_ZoomFactor(0.93)
		GA3M F 1 A_ZoomFactor(0.94)
		GA3M E 1 A_ZoomFactor(0.95)
		GA3M D 1 A_ZoomFactor(0.96)
		GA3M C 1 A_ZoomFactor(0.97)
		GA3M B 1 A_ZoomFactor(0.98)
		GA3M A 1 A_ZoomFactor(0.99)
		TNT1 A 0 A_ZoomFactor(1)
		TNT1 A 0 ACS_NamedExecuteAlways("SetPlayerSpeed",0,100,0,0)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
// ------------------------------------------------------------------------------------
//
// Mod Swap
//
// ------------------------------------------------------------------------------------

	StartUpgrader:
		TNT1 A 0 A_TakeInventory("StartUpgrader",1)
		TNT1 A 0 A_GiveInventory("UpgradeGaussCannon",1)
		TNT1 A 0 A_SelectWeapon("D4WeaponUpgrader")
		Goto WReady
		
	SwapMod:
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory("ModHasPrecisionScope", 1, "ToPrecision")
		TNT1 A 0 A_JumpIfInventory("ModHasSiegeMode", 1, "ToSiege")
		Goto NoUpgrades
		
		ToPrecision:
			TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"FromSiegeToPrecision")
			TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"PrecisionToNextCheck") 
			Goto FromNormalToSwap
			
			PrecisionToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasSiegeMode",1,"FromPrecisionToSiege")
				Goto NoOtherMod
			
		ToSiege:
			TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"FromPrecisionToSiege")
			TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"SiegeToNextCheck") 
			Goto FromNormalToSwap
			
			SiegeToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasPrecisionScope",1,"FromSiegeToPrecision")
				Goto NoOtherMod
		
	FromNormalToSwap:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		GA1G A 1 Offset(0,44)
		GA1G A 1 Offset(0,56)
		GA1G A 1 Offset(0,68)
		GA1G A 1 Offset(0,80)
		GA1G A 1 Offset(0,92)
		GA1G A 1 Offset(0,104)
		GA1G A 1 Offset(0,116)
		GA1G A 1 Offset(0,128)
		TNT1 A 5
		TNT1 A 0 A_JumpIfInventory("ModHasPrecisionScope",1,"SwapPrecision")
		TNT1 A 0 A_JumpIfInventory("ModHasSiegeMode",1,"SwapSiege") 
		Goto NoOtherMod
		
	FromPrecisionToSiege:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		GA2G A 1 Offset(0,44)
		GA2G A 1 Offset(0,56)
		GA2G A 1 Offset(0,68)
		GA2G A 1 Offset(0,80)
		GA2G A 1 Offset(0,92)
		GA2G A 1 Offset(0,104)
		GA2G A 1 Offset(0,116)
		GA2G A 1 Offset(0,128)
		TNT1 A 5
		TNT1 A 0 A_TakeInventory("ModPrecisionScopeActive", 1)
	SwapSiege:
		TNT1 A 0 A_GiveInventory("ModSiegeModeActive", 1)
		GA3T A 1 Offset(0,116)
		GA3T A 1 Offset(0,104)
		GA3T A 1 Offset(0,92)
		GA3T B 1 Offset(0,80)
		GA3T C 1 Offset(0,68)
		GA3T D 1 Offset(0,56)
		GA3T E 1 Offset(0,44)
		GA3T F 1 Offset(0,32) A_PlaySound("Gauss/SiegeMode", 5)
		GA3T GHIJKL 1
		GA3T M 3
		GA3T N 1
		GA3T O 1 Offset(1,32)
		GA3T P 1 Offset(4,33)
		GA3T Q 1 Offset(7,36)
		GA3T R 1 Offset(9,38)
		GA3T S 1 Offset(10,39)
		GA3T S 2 Offset(11,40)
		GA3T S 1 Offset(10,39)
		GA3T S 1 Offset(8,37)
		GA3T S 1 Offset(7,36)
		GA3T T 1 Offset(5,34)
		GA3T T 1 Offset(2,33)
		GA3T U 1 Offset(0,32)
		GA3T U 1 Offset(-2,32)
		GA3T V 1 Offset(-4,33)
		GA3T V 1 Offset(-6,35)
		GA3T W 1 Offset(-8,38)
		GA3T W 1 Offset(-10,41)
		GA3T X 1 Offset(-11,44)
		GA3T X 1 Offset(-12,46)
		GA3T Y 1 Offset(-11,44)
		GA3T Y 1 Offset(-9,41)
		GA3T Z 1 Offset(-5,38)
		GA3T Z 1 Offset(-1,33)
		GA3G A 1 Offset(0,32)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FromSiegeToPrecision:	
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		GA3G A 1 Offset(0,44)
		GA3G A 1 Offset(0,56)
		GA3G A 1 Offset(0,68)
		GA3G A 1 Offset(0,80)
		GA3G A 1 Offset(0,92)
		GA3G A 1 Offset(0,104)
		GA3G A 1 Offset(0,116)
		GA3G A 1 Offset(0,128)
		TNT1 A 5
		TNT1 A 0 A_TakeInventory("ModSiegeModeActive", 1)
	SwapPrecision:
		TNT1 A 0 A_GiveInventory("ModPrecisionScopeActive", 1)
		GA2T A 1 Offset(0,116)
		GA2T A 1 Offset(0,104)
		GA2T A 1 Offset(0,92)
		GA2T B 1 Offset(0,80)
		GA2T C 1 Offset(0,68)
		GA2T D 1 Offset(0,56)
		GA2T E 1 Offset(0,44)
		GA2T F 1 Offset(0,32)
		GA2T GHIJK 1
		GA2T L 3
		GA2T M 1
		GA2T N 1 A_playsound("Gauss/PrecisionMode", 5)
		GA2T O 1 Offset(1,32)
		GA2T P 1 Offset(4,33)
		GA2T P 1 Offset(7,36)
		GA2T P 1 Offset(9,38)
		GA2T P 1 Offset(10,39)
		GA2T P 2 Offset(11,40)
		GA2T P 1 Offset(10,39)
		GA2T P 1 Offset(8,37)
		GA2T P 1 Offset(7,36)
		GA2T Q 1 Offset(5,34)
		GA2T Q 1 Offset(2,33)
		GA2T R 1 Offset(0,33)
		GA2T S 1 Offset(-2,32)
		GA2T T 1 Offset(-4,33)
		GA2T U 1 Offset(-6,35)
		GA2T V 1 Offset(-8,38)
		GA2T W 1 Offset(-10,41)
		GA2T X 1 Offset(-11,44)
		GA2G A 1 Offset(-12,46)
		GA2G A 1 Offset(-11,44)
		GA2G A 1 Offset(-9,41)
		GA2G A 1 Offset(-5,38)
		GA2G A 1 Offset(-1,34)
		GA2G A 1 Offset(0,32)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady		

// ------------------------------------------------------------------------------------
//
// Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------
		
	PunchFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"PunchFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"PunchFlashPrecision")
		GA1S A 0
		Goto RealPunchFlash

		PunchFlashPrecision:
			GA2S A 0
			Goto RealPunchFlash
		PunchFlashSiege:
			GA3S A 0
			Goto RealPunchFlash		
			RealPunchFlash:
				"####" ECA 1
				Goto Flash
		
	KickFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"KickFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"KickFlashPrecision")
		GA1S A 0
		Goto RealKickFlash

		KickFlashPrecision:
			GA2S A 0
			Goto RealKickFlash
		KickFlashSiege:
			GA3S A 0
			Goto RealKickFlash
			RealKickFlash:
				"####" EDCB 1
				"####" A 8
				"####" BCDE 1
				Goto Flash

	AirKickFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"AirKickFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"AirKickFlashPrecision")
		GA1S A 0
		Goto RealAirKickFlash

		AirKickFlashPrecision:
			GA2S A 0
			Goto RealAirKickFlash
		AirKickFlashSiege:
			GA3S A 0
			Goto RealAirKickFlash
			RealAirKickFlash:
				"####" EDCB 1
				"####" A 10
				"####" BCDE 1
				Goto Flash
		
	SlideKickStartFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"SlideKickStartFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"SlideKickStartFlashPrecision")
		GA1S A 0
		Goto RealSlideKickStartFlash

		SlideKickStartFlashPrecision:
			GA2S A 0
			Goto RealSlideKickStartFlash
		SlideKickStartFlashSiege:
			GA3S A 0
			Goto RealSlideKickStartFlash
			RealSlideKickStartFlash:
				"####" EDCB 1
				"####" A 17
	SlideKickEndFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"SlideKickEndFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"SlideKickEndFlashPrecision")
		GA1S A 0
		Goto RealSlideKickEndFlash

		SlideKickEndFlashPrecision:
			GA2S A 0
			Goto RealSlideKickEndFlash
		SlideKickEndFlashSiege:
			GA3S A 0
			Goto RealSlideKickEndFlash
			RealSlideKickEndFlash:
				"####" A 5 
				"####" BCDE 1
				Goto Flash

	TauntFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"TauntFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"TauntFlashPrecision")
		GA1S A 0
		Goto RealTauntFlash

		TauntFlashPrecision:
			GA2S A 0
			Goto RealTauntFlash
		TauntFlashSiege:
			GA3S A 0
			Goto RealTauntFlash
			RealTauntFlash:
				"####" EDCB 1
				"####" A 35
				"####" BCDE 1
				Goto Flash
		
	GrenadeFlash:
		TNT1 A 0 A_JumpIfInventory("ModSiegeModeActive",1,"GrenadeFlashSiege")
		TNT1 A 0 A_JumpIfInventory("ModPrecisionScopeActive",1,"GrenadeFlashPrecision")
		GA1S A 0
		Goto RealGrenadeFlash
		GrenadeFlashPrecision:
			GA2S A 0
			Goto RealGrenadeFlash
		GrenadeFlashSiege:
			GA3S A 0
			Goto RealGrenadeFlash
			RealGrenadeFlash:
				"####" EDCBA 1
				Goto Flash
	}
}

Actor PrecisionCount1Second : Ammo 
{
	Inventory.Amount 0 
	inventory.maxamount 60
	Ammo.BackpackAmount 0 
	Ammo.BackpackMaxAmount 60
	Inventory.Icon "D4W7B0"
	Tag "Precision Count"
}
Actor GaussReadyZoomed : Inventory {inventory.maxamount 1}
Actor SiegeAltMode : Inventory {inventory.maxamount 1}

//
Actor GaussCannonExplosion
{
	+ALWAYSPUFF +NOINTERACTION +PUFFGETSOWNER +FORCERADIUSDMG
	DamageType ExplosiveImpact
	seesound "Gauss/Impact"
	Decal "Scorch"
	+NOBLOCKMAP
	+NOGRAVITY
	+NOEXTREMEDEATH
	+PUFFONACTORS
	+BLOODLESSIMPACT 
	+FORCEXYBILLBOARD
	+DONTSPLASH
	states
	{
	Spawn:
	Death:
	Melee:
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_playsound("Gauss/Impact", 3)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)TNT1 A 0 A_SpawnItem(GaussExplosionBack)
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 A 0 A_Explode(192,192,0,0,160)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 0 Radius_Quake (5, 10, 0, 10, 0)
		Stop
		
	}
}

Actor GaussCannonExplosionSIEGE : GaussCannonExplosion
{
speed 30
States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1
		TNT1 A 0 A_playsound("Gauss/Impact", 3)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItem(GaussExplosionBack)
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 A 0 A_Explode(192,384,0,0,160)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 10 Radius_Quake (5, 10, 0, 10, 0)
		Stop
	}
}

Actor GaussCannonExplosionSIEGE2 : GaussCannonExplosion
{
States
	{
	Spawn:
		TNT1 A 0
		BFE1 A 0 A_SpawnItemEx("BFGBlueSuperParticleSpawner", 0, 0, 0, 0, 0, 0, 0, 128)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBlueBIGFOG", 0, 0, random (0, 360), 2, random (0, 360))
		EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBlueBIGFOG", 0, 0, random (0, 360), 2, random (0, 360))
		EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBlueSuperParticle", 0, 0, random (0, 360), 2, random (0, 360))
		EXPL AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile ("BFGBlueSuperParticle", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_playsound("Gauss/Impact", 3)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItem(GaussExplosionBack)
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 A 0 A_Explode(192,192,0,0,160)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 10 Radius_Quake (5, 10, 0, 10, 0)
		Stop
	}
}

Actor GaussExplosionSprite : RandomExplosionSprite
{
Scale 0.72
Translation "0:255=%[0,0,0]:[0,0.75,1.00]"
}

Actor GaussExplosionBack : GaussCannonParticle
{
	Scale 10
	States
	{
	Spawn:
		MUZZ Q 1 Bright A_JumpIf(ScaleX<=0,"Null")
		TNT1 A 0 A_SetScale(ScaleX-1)
		Loop
	}
}
Actor GaussCannonExplosionShockWave : GaussCannonParticle
{
	Translation "0:255=%[0,0,0]:[0.78,1.54,1.95]"
	Scale 0.25
	States
	{
	Spawn:
		BSW1 A 1 Bright A_FadeOut(0.15)
		TNT1 A 0 A_SetScale(ScaleX*1.3)
		Loop
	}
}
Actor GaussCannonExplosionSpark : GaussCannonParticle
{
	+DONTSPLASH +MISSILE -NOINTERACTION +THRUACTORS
	Radius 1
	Height 1
	Scale 0.66
	Translation "0:255=%[0.00,0.00,0.00]:[1.07,1.65,2.00]"
	Gravity 0.66
	Speed 16
	States
	{
	Spawn:
		TNT1 AA 0 A_ScaleVelocity(FRandom(0.75,1.1))
		TNT1 A 0 A_SetScale(ScaleX*FRandom(0.75,1.2))
		NULL A 5 Bright
	Fade:
		NULL A 1 Bright A_JumpIf(ScaleX<=0,"Null")
		TNT1 A 0 A_SetScale(ScaleX-0.05)
		Loop
	}
}

ACTOR RipperSIEGE
{
	Radius 10
	Height 8
	Projectile
	Speed 500
    Damage 10
	DamageType Explosive
    MeleeDamage 0
    Gravity 0.00
	Scale 0.7
    Decal "Scorch"
    -NOGRAVITY
    +EXTREMEDEATH
    +BLOODSPLATTER 
	+THRUSPECIES
	+MTHRUSPECIES
	+RIPPER
	SeeSound "none"
	DeathSound "Gauss/Impact"
	Obituary "%o went boom"
	Species "Marines"
	States
	{
	Spawn:
		TNT1 A 0
	Spawn1:
	    TNT1 A 1 Bright
		Loop
	
	SpawnUnderwater:
	    TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
		TNT1 A 0 A_SpawnItem("RocketFlare",-20,0)
	    TNT1 A 0 A_CustomMissile ("BUBULZ", 0, 0, random (0, 360), 2, random (0, 180))
	    Goto Spawn1
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_playsound("Gauss/Impact", 3)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItem(GaussExplosionBack)
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 A 0 A_Explode(192,192,0,0,160)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 0 A_CustomMissile("RipperSIEGE", 0, 0, 0, 90)
		Stop
	Death:
        TNT1 A 0
		TNT1 A 0 A_playsound("Gauss/Impact", 3)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("LiquidExplosionEffectSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItem(GaussExplosionBack)
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 A 0 A_Explode(192,192,0,0,160)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 10 Radius_Quake (5, 10, 0, 10, 0)
		Stop
	}
}

Actor GaussCannonChargedParticle : EffectBase
{
	Scale 0.8
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+DONTSPLASH
	+THRUACTORS
	+NOTRIGGER
  +NOGRAVITY
	+NOBLOCKMONST
//	+NOTONAUTOMAP
	+CANNOTPUSH
	States
	{
	Spawn:
		TNT1 A 0 A_FadeOut(0.1)
		MUZZ H 1 Bright A_SetScale(ScaleX*0.95)
		Loop
	}
}
