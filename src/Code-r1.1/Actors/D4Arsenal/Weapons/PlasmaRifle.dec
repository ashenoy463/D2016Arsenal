ACTOR D4PlasmaRifle : D4WeaponBase replaces Plasma_Gun
{
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 5
	Weapon.AmmoType1 "D4Cells"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "PlasmaCharge"
    Inventory.PickupSound "PLSDRAW"
	Inventory.PickupMessage "You got the Plasma Rifle!"
	Obituary "%o was desinegrated by %k's Plasma Rifle."
    +WEAPON.NOALERT
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Tag "Plasma Rifle"
	States
	{
	Spawn:
		D4W6 A -1
		Stop		

	WReady:
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
		TNT1 A 0 A_JumpIfInventory("Reloading",1,"SwapMod")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"StartUpgrader")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "ReadyHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "ReadyStun")
		PLRG A 1 A_WeaponReady
		Loop
	
		ReadyStun:
			PL1G A 1 A_WeaponReady
			Goto WReady
		
		ReadyHeat:
			TNT1 A 0 A_JumpIfInventory("PlasmaCharge",100,"ReadyHeatFullCharged")
			TNT1 A 0 A_JumpIfInventory("PlasmaCharge",80,"ReadyHeatCharge80")
			TNT1 A 0 A_JumpIfInventory("PlasmaCharge",60,"ReadyHeatCharge60")
			TNT1 A 0 A_JumpIfInventory("PlasmaCharge",40,"ReadyHeatCharge40")
			TNT1 A 0 A_JumpIfInventory("PlasmaCharge",20,"ReadyHeatCharge20")
			PL2G A 1 A_WeaponReady
			Goto WReady			
			ReadyHeatCharge20:
				PL2G B 1 A_WeaponReady
				Goto WReady
			ReadyHeatCharge40:
				PL2G C 1 A_WeaponReady
				Goto WReady
			ReadyHeatCharge60:
				PL2G D 1 A_WeaponReady
				Goto WReady
			ReadyHeatCharge80:
				PL2G E 1 A_WeaponReady
				Goto WReady

			ReadyHeatFullCharged:
				TNT1 A 0 A_GiveInventory("PlasmaRifleFullChargedHandler",1)
				TNT1 A 0 A_JumpIfInventory("PlasmaRifleFullChargedHandler",5,"ReadyHeatFullCharged5")
				TNT1 A 0 A_JumpIfInventory("PlasmaRifleFullChargedHandler",4,"ReadyHeatFullCharged4")
				TNT1 A 0 A_JumpIfInventory("PlasmaRifleFullChargedHandler",3,"ReadyHeatFullCharged3")
				TNT1 A 0 A_JumpIfInventory("PlasmaRifleFullChargedHandler",2,"ReadyHeatFullCharged2")
				TNT1 A 0 A_JumpIfInventory("PlasmaRifleFullChargedHandler",1,"ReadyHeatFullCharged1")
			ReadyHeatFullCharged1:
				PL2G F 1 A_WeaponReady
				Goto WReady
			ReadyHeatFullCharged2:
				PL2G G 1 A_WeaponReady
				Goto WReady
			ReadyHeatFullCharged3:
				PL2G H 1 A_WeaponReady
				Goto WReady
			ReadyHeatFullCharged4:
				PL2G H 1 A_WeaponReady
				Goto WReady
			ReadyHeatFullCharged5:
				TNT1 A 0 A_TakeInventory("PlasmaRifleFullChargedHandler",32767)
				PL2G H 1 A_WeaponReady
				Goto WReady

	Deselect:
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		TNT1 A 0 A_Takeinventory("PlasmaRifleFullChargedHandler",32767)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "DeselectHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "DeselectStun")
		PLRS A 0
		Goto RealDeselect
		
		DeselectStun:
			PL1S A 0
			Goto RealDeselect			
		DeselectHeat:
			TNT1 A 0 A_TakeInventory("WeaponUnFlashEnabled", 1)//Handler for Heat Blast Effect
			PL2S A 0 A_GunFlash("UnFlash")
			Goto RealDeselect
		
	RealDeselect:
		"####" EDCBA 1
		"####" A 0
		Goto DeselectSequence
		
	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("PlasmaGunSelected",1)
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "StartSelectHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "StartSelectStun")
		PLRS A 0
		Goto SelectSequence		
		StartSelectStun:
			PL1S A 0
			Goto SelectSequence		
		StartSelectHeat:
			PL2S A 0
			Goto SelectSequence
	
	Ready:
		TNT1 A 0 A_PlaySound("PLSDRAW")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "SelectHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "SelectStun")
		PLRS A 0
		Goto RealSelect
		
		SelectStun:
			PL1S A 0
			Goto RealSelect		
		SelectHeat:
			TNT1 A 0 A_GiveInventory("WeaponUnFlashEnabled", 1)//Handler for Heat Blast Effect
			PL2S A 0
			Goto RealSelect
		
	RealSelect:
		"####" ABCDE 1
		TNT1 A 0 A_GunFlash // Starts here because for the Heat Blast Effect
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
// ------------------------------------------------------------------------------------
//
// Fire - Related
//
// ------------------------------------------------------------------------------------
		
	Fire:
        "####" A 0 A_JumpIfInventory("D4Cells",1,1)
        Goto NoAmmo
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "FireHeat")	
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "FireStun")
		PLRF A 0
		Goto FireRelease
		
		FireStun:
			PL1F A 0
			Goto FireRelease			
				
	FireRelease:
		"####" A 0 A_GunFlash("FirePrimary")
		"####" A 1 BRIGHT Offset(4,38)
		"####" B 1 BRIGHT Offset(2,35)
		"####" "#" 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,1)
		"####" D 1 Offset(1,33)
		"####" A 0 A_Refire("Fire")
		Goto EndFire
		
	EndFire:
		"####" A 0 A_JumpIfInventory("PlasmaRifleFired",2,"LastPlasmaSound") //if it had 2, means it was looping and needs to shut up
		"####" A 0 A_TakeInventory("PlasmaRifleFired",2)
		Goto EndFireContinue
	
	EndFireContinue:
		"####" A 0 A_PlaySound("PlasmaRifle/StopMain", CHAN_WEAPON) 
		"####" D 3 A_StopSound(5)
        "####" A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	LastPlasmaSound:
		"####" A 0 A_PlaySound("PlasmaRifle/Fire",CHAN_WEAPON,1)
		Goto Endfire+1
		
	FirePrimary:
		TNT1 A 0 A_FireCustomMissile("BlueFlareSpawn",0,0,3,0)
        TNT1 A 0 A_FireCustomMissile("D4PlasmaBall",0,0,3,1.5)
		TNT1 A 0 A_SetBlend("skyblue",0.03,3)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetPitch(pitch-0.8)
		TNT1 A 0 A_Takeinventory("D4Cells",1)
		TNT1 A 0 A_JumpIfInventory("PlasmaRifleFired",1,"PrimNormFlashSndLooper")
		TNT1 A 0 A_PlaySound("PlasmaRifle/Fire",CHAN_WEAPON,1)
		Goto PrimaryNormalFlashContinue
		PrimaryNormalFlashContinue:
			TNT1 A 1 A_GiveInventory("PlasmaRifleFired",1) //1 tc delay to readjust pitch
			TNT1 A 0 A_SetPitch(pitch+0.8)
			Goto Flash
		PrimNormFlashSndLooper:
			TNT1 A 0 A_PlaySound("PlasmaRifle/FireLoop",5,1,2)
			Goto PrimaryNormalFlashContinue
		
	NoAmmo:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "NoAmmoHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "NoAmmoStun")
		PLRG A 0
		Goto DryFire
		
		NoAmmoStun:
			PL1G A 0
			Goto DryFire		
		NoAmmoHeat:
			PL2G A 0
			Goto DryFire
			
	DryFire:
		"####" A 0 A_PlaySound("PlasmaRifle/StopMain", CHAN_WEAPON) 
		"####" A 0 A_StopSound(5)
		"####" A 1 A_PlaySound("BEP",1)
		Goto WReady
	
	// Heat Blast Fire
	FireHeat:
		PL2F A 0
		"####" A 0 A_FireCustomMissile("BlueFlareSpawn",0,0,3,0)
        "####" A 0 A_FireCustomMissile("D4PlasmaBall",0,0,3,1.5)
		"####" A 0 A_SetBlend("skyblue",0.03,3)
		"####" A 0 A_AlertMonsters
		"####" A 0 A_SetPitch(pitch-0.8)
		"####" A 0 A_Takeinventory("D4Cells",1)
		"####" A 0 A_JumpIfInventory("PlasmaRifleFired",1,"FireHeatSndLooper")
		"####" A 0 A_PlaySound("PlasmaRifle/Heat/Fire",CHAN_WEAPON,1)	
		Goto ContinueFireHeat

		FireHeatSndLooper:
			"####" A 0 A_PlaySound("PlasmaRifle/Heat/FireLoop",5,1,2)
			Goto ContinueFireHeat	
			
	ContinueFireHeat:
		"####" A 0 A_Giveinventory("PlasmaCharge",1)
		"####" A 1 BRIGHT Offset(4,38) A_GiveInventory("PlasmaRifleFired",1) //1 tc delay to readjust pitch
		"####" A 0 A_SetPitch(pitch+0.8)
		"####" B 1 BRIGHT Offset(2,35)
		"####" "#" 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,1)
		"####" D 1 Offset(1,33)
		"####" A 0 A_Refire("Fire")
		Goto EndFireHeat
		
	EndFireHeat:
		"####" A 0 A_JumpIfInventory("PlasmaRifleFired",2,"LastPlasmaSoundHeat") //if it had 2, means it was looping and needs to shut up
		"####" A 0 A_TakeInventory("PlasmaRifleFired",2)
		Goto EndFireContinue
		
	LastPlasmaSoundHeat:
		"####" A 0 A_PlaySound("PlasmaRifle/Heat/Fire",CHAN_WEAPON,1)
		Goto EndfireHeat+1
		
// ------------------------------------------------------------------------------------
//
// Alt Fire
//
// ------------------------------------------------------------------------------------
		
	AltFire:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "StunFire")
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "AltFireHeat")
		Goto NoUpgrades
		
	StunFire:
		TNT1 A 0 A_JumpIfInventory("D4Cells",10,1)
		Goto NoAmmo
		TNT1 A 0 A_JumpIfInventory("PlasmaStunBombTimer",1,"ModNotReady")
		TNT1 A 0 A_GiveInventory("PlasmaStunBombTimer",52)
		TNT1 A 0 ACS_NamedExecuteAlways("PlasmaStunBombTimer",0,0,0,0)
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		PLSM A 0 A_PlaySound("PlasmaRifle/Stun/Fire", 6)
		TNT1 A 0 A_JumpIfInventory("IsPlayingAsDM",1,"StaticBall")
		PL1F A 1 BRIGHT A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("BlueFlareSpawn",0,0,0,0)
        RIFF A 0 A_FireCustomMissile("StunBall", 0, 0, 4, -4, 0, 2)
		PL1F BC 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		Goto FinishStun
		
	StaticBall:
		PL1C A 1 BRIGHT A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("BlueFlareSpawn",0,0,0,0)
        RIFF A 0 A_FireCustomMissile("HeatBall", 0, 0, 4, -4, 0, 2)
		PL1C EF 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		Goto FinishStun
		
	FinishStun:
		TNT1 A 0 A_Takeinventory("D4Cells",10)
		PL1C DDDDDEEFF 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		PL1G BBBBBBBBBB 1 BRIGHT A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	AltFireHeat:
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge",10,"AltFireHeatBlast")
		Goto ModNotReady

	AltFireHeatBlast:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		PLSM A 0 A_PlaySound("PlasmaRifle/Heat/Explode", 4)
		PL2C A 1 BRIGHT A_AlertMonsters
		TNT1 A 0 A_GunFlash("HeatBlastFireFlash")
		TNT1 A 0 A_Takeinventory("PlasmaCharge",100)
		PL2C BCD 1 BRIGHT 
		PL2C EEFFGG 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	HeatBlastFireFlash:
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 0, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 16, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 32, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 64, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 80, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 96, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 112, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 128, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 144, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 160, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 176, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 192, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 208, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 224, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 240, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 256, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 272, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 288, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 304, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 320, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 336, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_FireCustomMissile("HotPlasmaGas2", 352, 0, 0, 0)//, FPF_NOAUTOAIM)
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 100, "HBFF.Damage100")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 90, "HBFF.Damage90")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 80, "HBFF.Damage80")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 70, "HBFF.Damage70")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 60, "HBFF.Damage60")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 50, "HBFF.Damage50")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 40, "HBFF.Damage40")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 30, "HBFF.Damage30")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 20, "HBFF.Damage20")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 10, "HBFF.Damage10")
		Goto Flash
	HBFF.Damage100:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage100", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage90:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage90", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage80:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage80", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage70:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage70", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage60:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage60", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage50:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage50", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage40:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage40", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage30:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage30", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage20:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage20", 0, 0, 0, 0)	Goto Flash
	HBFF.Damage10:	TNT1 A 0 A_FireCustomMissile("HeatBlastDamage10", 0, 0, 0, 0)	Goto Flash
		
// ------------------------------------------------------------------------------------
//
// Mod Swap
//
// ------------------------------------------------------------------------------------
		
	StartUpgrader:
		TNT1 A 0 A_TakeInventory("StartUpgrader",1)
		TNT1 A 0 A_GiveInventory("UpgradePlasmaRifle",1)
		TNT1 A 0 A_SelectWeapon("D4WeaponUpgrader")
		Goto WReady
		
	SwapMod:
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory("ModHasStunBomb", 1, "ToStunBomb")
		TNT1 A 0 A_JumpIfInventory("ModHasHeatWave", 1, "ToHeatBlast")
		Goto NoUpgrades
		
		ToStunBomb:
			TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive",1,"FromHeatBlastToStunBomb")
			TNT1 A 0 A_JumpIfInventory("ModStunBombActive",1,"StunBombToNextCheck") 
			Goto FromNormalToSwap
			
			StunBombToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasHeatWave",1,"FromStunBombToHeatBlast")
				Goto NoOtherMod
			
		ToHeatBlast:
			TNT1 A 0 A_JumpIfInventory("ModStunBombActive",1,"FromStunBombToHeatBlast")
			TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive",1,"HeatBlastToNextCheck") 
			Goto FromNormalToSwap
			
			HeatBlastToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasStunBomb",1,"FromHeatBlastToStunBomb")
				Goto NoOtherMod
		
	FromNormalToSwap:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		PLRG A 1 Offset(0,52)
		PLRG A 1 Offset(0,72)
		PLRG A 1 Offset(0,92)
		PLRG A 1 Offset(0,112)
		PLRG A 5	
		TNT1 A 0 A_JumpIfInventory("ModHasStunBomb", 1, "SwapStunBomb")
		TNT1 A 0 A_JumpIfInventory("ModHasHeatWave", 1, "SwapHeatBlast")
		Goto NoOtherMod
		
	FromStunBombToHeatBlast:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		PL1G A 1 Offset(0,52)
		PL1G A 1 Offset(0,72)
		PL1G A 1 Offset(0,92)
		PL1G A 1 Offset(0,112)
		PL1G A 5	
	SwapHeatBlast:
		TNT1 A 0 A_TakeInventory("ModStunBombActive", 1)
		TNT1 A 0 A_playsound("PlasmaRifle/Heat/Toggle", 4)
		PL2T A 1 Offset(0,102)
		PL2T A 1 Offset(0,92)
		PL2T A 1 Offset(0,82)
		PL2T A 1 Offset(0,72)
		PL2T A 1 Offset(0,62)
		PL2T A 1 Offset(0,52)
		PL2T B 1 Offset(0,42)
		PL2T C 1 Offset(0,32)
		PL2T DEFGHIJK 1
		PL2T K 1 Offset(0,35)
		PL2T K 1 Offset(0,38)
		PL2T K 1 Offset(0,41)
		PL2T K 1 Offset(0,44)
		PL2T K 10
		PL2T L 2 Offset(0,41)
		PL2T M 2 Offset(0,38)
		PL2T N 2 Offset(0,35)
		PL2T O 2 Offset(0,32)
		TNT1 A 0 A_GiveInventory("ModHeatWaveActive", 1)
		TNT1 A 0 A_GiveInventory("WeaponUnFlashEnabled", 1)//Handler for Heat Blast Effect
		PL2G A 8 
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FromHeatBlastToStunBomb:	
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		PL2G A 1 Offset(0,52)
		PL2G A 1 Offset(0,72)
		PL2G A 1 Offset(0,92)
		PL2G A 1 Offset(0,112)
		PL2G A 5	
		TNT1 A 0 A_TakeInventory("WeaponUnFlashEnabled", 1)//Handler for Heat Blast Effect
		TNT1 A 0 A_TakeInventory("ModHeatWaveActive", 1)
	SwapStunBomb:
		TNT1 A 0 A_GiveInventory("ModStunBombActive", 1)
		PL1T A 1 Offset(0,102)
		PL1T A 1 Offset(0,92)
		PL1T A 1 Offset(0,82)
		PL1T A 1 Offset(0,72)
		PL1T A 1 Offset(0,62)
		PL1T B 1 Offset(0,52)
		PL1T C 1 Offset(0,42)
		PL1T D 1 Offset(0,32)
		TNT1 A 0 A_playsound("PlasmaRifle/Stun/Toggle", 4)
		PL1T EFGHIJK 1
		PL1T L 1 Offset(0,35)
		PL1T L 1 Offset(0,38)
		PL1T L 1 Offset(0,41)
		PL1T L 1 Offset(0,44)
		PL1T L 5
		PL1T M 2 Offset(0,41)
		PL1T N 2 Offset(0,38)
		PL1T O 2 Offset(0,35)
		PL1T P 2 Offset(0,32)
		PL1G A 8 
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
// ------------------------------------------------------------------------------------
//
// Heat Blast Flash Effect
//
// ------------------------------------------------------------------------------------

	// This State is ancled into the Heat Blast Effect Checker
	Flash:
		TNT1 A 1 A_JumpIfInventory("ModHeatWaveActive", 1, "HeatBlastFX")
		Loop
		
	// Ready Heat Blast Effect Checker
	HeatBlastFX:
		TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive",1,1)
		Goto Flash
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 100, "HeatBlastFXFull")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 80, "HeatBlastFX80")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 60, "HeatBlastFX60")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 40, "HeatBlastFX40")
		TNT1 A 0 A_JumpIfInventory("PlasmaCharge", 20, "HeatBlastFX20")
		TNT1 A 1 //No Charge, no effect
		Loop
		
	HeatBlastFX80:
		PL21 D 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFX60:
		PL21 C 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFX40:
		PL21 B 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFX20:
		PL21 A 1 BRIGHT
		Goto HeatBlastFX
		
	HeatBlastFXFull:
		TNT1 A 0 A_Jump(100, "HeatBlastFXFull1")
		TNT1 A 0 A_Jump(100, "HeatBlastFXFull2")
		TNT1 A 0 A_Jump(100, "HeatBlastFXFull3")
		TNT1 A 0 A_Jump(100, "HeatBlastFXFull4")
		TNT1 A 0 A_Jump(100, "HeatBlastFXFull5")
		TNT1 A 0 A_Jump(100, "HeatBlastFXFull6")
		PL21 E 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFXFull1:
		PL21 F 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFXFull2:
		PL21 G 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFXFull3:
		PL21 H 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFXFull4:
		PL21 I 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFXFull5:
		PL21 J 1 BRIGHT
		Goto HeatBlastFX
	HeatBlastFXFull6:
		PL21 K 1 BRIGHT
		Goto HeatBlastFX
		
// ------------------------------------------------------------------------------------
//
// Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------
	
	PunchFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "PunchFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "PunchFlashStun")
		PLRK A 0
		Goto RealPunchFlash

		PunchFlashStun:
			PL1K A 0
			Goto RealPunchFlash
		PunchFlashHeat:
			PL2K A 0
			Goto RealPunchFlash
			RealPunchFlash:
				"####" ACE 1
				"####" A 0 A_JumpIfInventory("WeaponUnFlashEnabled", 1, "UnFlash")
				Goto Flash
		
	KickFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "KickFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "KickFlashStun")
		PLRK A 0
		Goto RealKickFlash
		
		KickFlashStun:
			PL1K A 0
			Goto RealKickFlash
		KickFlashHeat:
			PL2K A 0
			Goto RealKickFlash
			RealKickFlash:
				"####" ABCD 1
				"####" E 8
				"####" DCBA 1
				Goto Flash

	AirKickFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "AirKickFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "AirKickFlashStun")
		PLRK A 0
		Goto RealAirKickFlash

		AirKickFlashStun:
			PL1K A 0
			Goto RealAirKickFlash
		AirKickFlashHeat:
			PL2K A 0
			Goto RealAirKickFlash
			RealAirKickFlash:
				"####" ABCD 1
				"####" E 10
				"####" DCBA 1
				Goto Flash
		
	SlideKickStartFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "SlideKickStartFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "SlideKickStartFlashStun")
		PLRS A 0
		Goto RealSlideKickStartFlash
		
		SlideKickStartFlashStun:
			PL1S A 0
			Goto RealSlideKickStartFlash		
		SlideKickStartFlashHeat:
			PL2S A 0
			Goto RealSlideKickStartFlash
			RealSlideKickStartFlash:
				"####" EDCB 1
				"####" A 17
	SlideKickEndFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "SlideKickEndFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "SlideKickEndFlashStun")
		PLRS A 0
		Goto RealSlideKickEndFlash
		SlideKickEndFlashHeat:
			PL2S A 0
			Goto RealSlideKickEndFlash
		SlideKickEndFlashStun:
			PL1S A 0
			Goto RealSlideKickEndFlash
			RealSlideKickEndFlash:
				"####" A 5
				"####" BCDE 1
				Goto Flash

	TauntFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "TauntFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "TauntFlashStun")
		PLRK A 0
		Goto RealTauntFlash

		TauntFlashStun:
			PL1K A 0
			Goto RealTauntFlash
		TauntFlashHeat:
			PL2K A 0
			Goto RealTauntFlash
			RealTauntFlash:
				"####" ABCD 1
				"####" E 35
				"####" DCBA 1
				Goto Flash
		
	GrenadeFlash:
        TNT1 A 0 A_JumpIfInventory("ModHeatWaveActive", 1, "GrenadeFlashHeat")
        TNT1 A 0 A_JumpIfInventory("ModStunBombActive", 1, "GrenadeFlashStun")
		PLRK A 0
		Goto RealGrenadeFlash
		
		GrenadeFlashStun:
			PL1K A 0
			Goto RealGrenadeFlash
		GrenadeFlashHeat:
			PL2K A 0
			Goto RealGrenadeFlash
			RealGrenadeFlash:
				"####" ABCDE 1
				"####" A 0 A_JumpIfInventory("WeaponUnFlashEnabled", 1, "UnFlash")
				Goto Flash
	}
}

ACTOR PlasmaCharge : Ammo
{
   Inventory.Amount 0
   Inventory.MaxAmount 100
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 100
   Inventory.Icon "D4W6A0"
   Tag "Plasma Charge"  
}

Actor PlasmaRifleFullChargedHandler : Inventory {inventory.maxamount 5}
Actor PlasmaRifleFired : Inventory {inventory.maxamount 2}

ACTOR D4PlasmaBall
{
	Scale 0.2
	Height 8
	Radius 4
	Speed 75
	Damage (random(40, 40))
	Projectile
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1
	DeathSound "PlasmaProjectile/Explode"
	Obituary "%o was melted by %k's plasma rifle."
	Decal "SmallerScorch"
	DamageType Plasma
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx(PlasmaBallTail,-VelX,-VelY,-VelZ+1.5,VelX,VelY,VelZ,Angle,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEPOSITION)
		PB2A A 1 Bright A_SpawnItemEx(PlasmaBallGlow,0,0,1)
		Loop
	Death:
		TNT1 A 0 A_CustomMissile ("BlueFlare3", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_PlaySound("PlasmaProjectile/Explode")//Not sure if this will be necessary anymore...it is
		TNT1 A 1 A_Explode(5,50,1)
		TNT1 A 0 A_SpawnItemEx ("DetectFloorCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SpawnItemEx ("DetectCeilCraterSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		PLSY A 0 Bright
		TNT1 A 0 A_SpawnItem("PlasmaBallImpactShockWave")
		TNT1 A 0 A_Jump(64,2,3)
		TNT1 AA 0 A_SpawnItem("GenericBolt")
		TNT1 A 0 A_SetScale(FRandom(0.33,0.75))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		BPLY ABC 1 Bright A_Explode(3,50,0)
		BPLY DEFGHI 1 Bright
		Stop
	}
}

ACTOR StunBall : ArachnotronPlasma
{
	Damage 10
	SeeSound ""
	Speed 30
	-NOGRAVITY +FORCEXYBILLBOARD// +FORCEPAIN
	Gravity 0.5
	DeathSound "PlasmaRifle/Stun/Impact"
	Obituary "%k tased the hell outta %o."
	Scale 0.25
	renderstyle add
	Alpha 99
	DamageType Stun
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0.5),Random(1,360))
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,-MomX*0.5,-MomY*0.5,-MomZ*0.5,FRandom(-0.5,0.5),Random(-0.5,0.5),FRandom(-0.5,0.5),0,SXF_ABSOLUTEPOSITION)
		TNT1 AA 0 A_SpawnItemEx(StunBallTrail,Random(0,10),0,Random(-10,10),FRandom(0,0.1),0,FRandom(-0.1,0.1),Random(1,360))
		STBL A 1 A_SpawnItemEx("PlasmaFlare")//AAAABBBBCCCC 1 Bright// A_SpawnItem(StunBallTrail)
		
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0.5),Random(1,360))
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,-MomX*0.5,-MomY*0.5,-MomZ*0.5,FRandom(-0.5,0.5),Random(-0.5,0.5),FRandom(-0.5,0.5),0,SXF_ABSOLUTEPOSITION)
		TNT1 AA 0 A_SpawnItemEx(StunBallTrail,Random(0,10),0,Random(-10,10),FRandom(0,0.1),0,FRandom(-0.1,0.1),Random(1,360))
		STBL A 1 A_SpawnItemEx("PlasmaFlare")//AAAABBBBCCCC 1 Bright// A_SpawnItem(StunBallTrail)
		Loop
	Death:
	XDeath:
		TNT1 A 0
		//TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(ShockSpark,0,0,0,FRandom(5,10),0,Random(5,10),Random(1,360),SXF_TRANSFERSCALE|1)
		TNT1 A 0 A_SpawnItemEx(GenericBolt,Random(0,20),0,0,0,0,0,Random(1,360),SXF_TRANSFERSCALE|1)
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx(StunBallSplash,0,0,0,FRandom(0,4),0,FRandom(4,6),Random(1,360))
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 0 A_Explode(2,160,0,0,160)
		Stop
	}
}

Actor HeatBlastDamage100 //Base
{
	+PUFFONACTORS
	+ALWAYSPUFF
	+NOINTERACTION
	+PUFFGETSOWNER
	+FORCERADIUSDMG
	+INVULNERABLE
	+NOINTERACTION
	+NOTONAUTOMAP
	+FORCERADIUSDMG
	+FRIENDLY
	RenderStyle None
	DamageType Plasma
	Species "Marines"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(400, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage90 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(360, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage80 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(320, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage70 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(280, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage60 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(240, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage50 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(200, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage40 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(160, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage30 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(120, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage20 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(80, 150,0,0,140)
		Stop
	}
}

Actor HeatBlastDamage10 : HeatBlastDamage100
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode(10, 150,0,0,140)
		Stop
	}
}

Actor HotPlasmaGas2 : BluePlasmaParticle
{
	Speed 9
	States
	{
	Spawn:
		FIR5 AB 1 BRIGHT
		FIR5 CDEFGH 1 BRIGHT A_CustomMissile ("BluePlasmaFireMove", 0, 0, 0, 2, random (-30, 30))
		FIR5 IJKL 1 BRIGHT A_CustomMissile ("BluePlasmaFireMove2", 0, 0, 0, 2, random (-60, 60))
		FIR5 MNOP 1 Bright A_CustomMissile ("BluePlasmaFireMove2", 0, 0, random (0, 360), 2, random (0, 360))
		Stop
	Death:
		TNT1 A 0 A_Explode(5, 180, 0)
		FIR5 BCDEFGHIJKLMNOP 1 Bright A_CustomMissile ("BluePlasmaFireMove2", 0, 0, random (0, 360), 2, random (0, 360))
		Stop
	}
}

ACTOR HeatBall : FatShot
{
	Damage 30
	Damagetype Fire
	Speed 25
	-NOGRAVITY
	Gravity 0.33
	//SeeSound "Misc/insideaboxwithoutspeakers"
	DeathSound "PlasmaRifle/Stun/Impact"
	Obituary "%k turned %o into roast meat."
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx(HeatBallTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0.5),Random(1,360))
		TNT1 A 0 A_SpawnItemEx(HeatBallTrail,-MomX*0.5,-MomY*0.5,-MomZ*0.5,FRandom(-0.5,0.5),Random(-0.5,0.5),FRandom(-0.5,0.5),0,SXF_ABSOLUTEPOSITION)
		TNT1 AA 0 A_SpawnItemEx(HeatBallTrail2,Random(0,10),0,Random(-10,10),FRandom(0,0.1),0,FRandom(-0.1,0.1),Random(1,360))
		TNT1 A 1 A_SpawnItemEx(RocketFlare)
		Loop
	Death:
		TNT1 AAA 0 A_SpawnItemEx(HeatBallWave,0,0,0,Random(0,3),0,FRandom(-3,3),Random(1,360))
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx(HeatBallSplash,0,0,0,FRandom(0,4),0,FRandom(4,6),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(HeatBallSparkSpawner,Random(2,96),0,0,0,0,-5,Random(1,360))
		TNT1 A 0 A_SetScale(0.09)
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(ShockSpark,0,0,0,FRandom(5,10),0,Random(5,10),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_PlaySound("PlasmaRifle/Stun/Burn",0)
		TNT1 A 0 A_ChangeFlag(NODAMAGETHRUST,1)
		TNT1 A 0 A_ChangeFlag(BLOODLESSIMPACT,1)
	Burn:
		TNT1 A 0 A_Explode(10,96,XF_HURTSOURCE,0,64)
		TNT1 AA 0 A_Explode(10,96,0,0,64)
		TNT1 A 16 A_JumpIfInventory(Clip,15,"Null")
		TNT1 A 0 A_GiveInventory(Clip,1)
		Loop
	}
}