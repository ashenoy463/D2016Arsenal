ACTOR D4HAR : D4WeaponBase Replaces Machinegun
{
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse1 0
	Weapon.AmmoGive1 20
	Weapon.AmmoType1 "D4Bullets"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
    Inventory.PickupSound "CBOXPKUP"
	Inventory.PickupMessage "You got the Heavy Assault Rifle!"
	Obituary "%o was rapidly fired by %k."
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Tag "Heavy Assault Rifle"
	States
	{
	WReady:
		// ------------ Checks for Zoomed State ------------
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"ReadyZoomed")
		// -------------------------------------------------
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DualWieldAnimChecker")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"DualWieldAnimChecker")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"DualWieldAnimChecker")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"StartUpgrader")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"ReadyDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"ReadyMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"ReadyScope")
		MAGG A 1 A_WeaponReady
		Loop	

		ReadyScope:
			M2GG A 1 A_WeaponReady
			Goto WReady		
		ReadyMissile:
			"####" "#" 0 A_JumpIfInventory("HAROpenHatch", 1, "ReadyOpenHatch")
			M3GG A 1 A_WeaponReady
			Goto WReady
		ReadyOpenHatch:
			TNT1 A 0 A_JumpIfInventory("FiredMissile", 5, "ReadyOH1")
			TNT1 A 0 A_JumpIfInventory("FiredMissile", 4, "ReadyOH2")
			TNT1 A 0 A_JumpIfInventory("FiredMissile", 3, "ReadyOH3")
			TNT1 A 0 A_JumpIfInventory("FiredMissile", 2, "ReadyOH4")
			TNT1 A 0 A_JumpIfInventory("FiredMissile", 1, "ReadyOH5")
			M3GB A 1 A_WeaponReady
			Goto WReady
			
			ReadyOH5:
				M3GB D 1 A_WeapoNReady
				Goto WReady
			ReadyOH4:
				M3GB G 1 A_WeapoNReady
				Goto WReady
			ReadyOH3:
				M3GB J 1 A_WeapoNReady
				Goto WReady
			ReadyOH2:
				M3GB M 1 A_WeapoNReady
				Goto WReady
			ReadyOH1:
				M3GB P 1 A_WeapoNReady
				Goto WReady
		
	ReadyZoomed:
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"NoAimFast")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"NoAimFast")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"NoAimFast")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "NoAimFast")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"NoAimFast")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"NoAimFast")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"NoAimFast")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "NoAimFast")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "NoAimFast")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"NoAimFast")
		TNT1 A 0 A_JumpIfInventory("StartDualWield",1,"NoAimFast")
		R016 M 1 BRIGHT A_WeaponReady
		Goto WReady
		
	Flash:
		TNT1 A 1 A_JumpIfInventory("HARDualWield",1,"RightWeapon")
		Loop

	Deselect:
		TNT1 A 0 A_JumpIfInventory("ChecksIsFiringTheRightGun",1,"WReady")
		TNT1 A 0 ACS_NamedTerminate("DualWieldChecker")
		TNT1 A 1 A_JumpIfInventory("HARDualWield",1,1)
		Goto CheckForMicroMissile
        TNT1 A 0 A_TakeInventory("IsDualWeapon",1)
		TNT1 A 0 A_TakeInventory("HARDualWield",1)
		TNT1 A 0 A_GunFlash("FakeDeselect")
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
		M1GS EDCBA 1
		M1GS A 0
		Goto DeselectSequence
		
	CheckForMicroMissile:
		TNT1 A 0 A_JumpIfInventory("HAROpenHatch",1,1)
		Goto CheckForTacticalScope
		TNT1 A 0 A_PlaySound("MICROEND", 4)
		M3GA ECA 1
		TNT1 A 0 A_TakeInventory("FiredMissile", 6)
		TNT1 A 0 A_TakeInventory("HAROpenHatch", 1)
		Goto ContinueforDeselection
		
	CheckForTacticalScope:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,1)
		Goto ContinueForDeselection
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
        R016 M 1 BRIGHT
        M2GA BA 1
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		Goto ContinueforDeselection
		
	ContinueforDeselection:
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"DeselectMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"DeselectScope")
		MAGS A 0
		Goto RealDeselect

		DeselectMissile:
			M3GS A 0
			Goto RealDeselect
		DeselectScope:
			M2GS A 0
			Goto RealDeselect
		
	RealDeselect:
		"####" EDCBA 1
		"####" A 0
		Goto DeselectSequence
		
	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"StartSelectMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"StartSelectScope")
		MAGS A 0
		Goto SelectSequence
		StartSelectScope:
			M2GS A 0
			Goto SelectSequence	
		StartSelectMissile:
			M3GS A 0
			Goto SelectSequence

	Ready:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 ACS_NamedExecuteAlways("DualWieldChecker")
		TNT1 A 0 A_PlaySound("Weapon/Switch")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"DeployDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"SelectMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"SelectScope")
		MAGS A 0
		Goto RealSelect
		
		SelectScope:
			M2GS A 0
			Goto RealSelect	
		SelectMissile:
			M3GS A 0
			Goto RealSelect
		
	RealSelect:
		"####" ABCDE 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
// ------------------------------------------------------------------------------------
//
// Fire
//
// ------------------------------------------------------------------------------------
		
	Fire:
		"####" "#" 0 A_JumpIfInventory("HAROpenHatch", 1, "FireMicro")
		R016 M 0 A_JumpIfInventory("Zoomed",1,"FireZoomed")
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"FireDualWield")
		TNT1 A 0 A_JumpIfInventory("D4Bullets",1,1)
		Goto NoAmmo
		TNT1 A 0 A_GunFlash("FirePrimary")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"FireMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"FireScope")
		MAGF A 0
		Goto FireRelease
		
		FireScope:
			M2GF A 0
			Goto FireRelease
		FireMissile:
			M3GF A 0
			Goto FireRelease		
		
	FireRelease:
		"####" A 1 BRIGHT 
		"####" B 1 BRIGHT Offset(4,36)
		"####" C 1 BRIGHT Offset(2,34)
		"####" "#" 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"FinishFire")
		"####" D 1 BRIGHT Offset(1,32)
		Goto FinishFire
		
	FinishFire:
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"FinishFireMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"FinishFireScope")
		MAGG A 0
		Goto FinishFireRelease
		
		FinishFireScope:
			M2GG A 0
			Goto FinishFireRelease
		FinishFireMissile:
			M3GG A 0
			Goto FinishFireRelease
	
	FinishFireRelease:
		"####" "#" 0 A_SetPitch(0.6 + pitch)
		"####" "#" 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,1)
		"####" "#" 1
		"####" "#" 1 A_Refire("Fire")
		"####" "#" 5 A_WeaponReady
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	NoAmmo:
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"NoAmmoMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"NoAmmoScope")
		MAGG A 0
		Goto DryFire
		
		NoAmmoScope: 
			M2GG A 0
			Goto DryFire
		NoAmmoMissile:
			M3GG A 0
			Goto DryFire
	DryFire:
		"####" "#" 1 A_PlaySound("weapons/empty")
		Goto WReady	
		
	FirePrimary:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 A 0 A_JumpIfInventory("HAROpenHatch", 1, "FireMicroMissile")
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"FireSoundChanZoomed")
		Goto FireSoundChannel
		// -------------- Fire Sound Checker --------------
		FireSoundChannel:
			TNT1 A 0 A_JumpIfInventory("HARFireSound",1,3)
			TNT1 A 0 A_GiveInventory("HARFireSound", 1)
			TNT1 A 0 A_PlaySound("HAR/Fire", CHAN_5)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_TakeInventory("HARFireSound", 1)
			TNT1 A 0 A_PlaySound("HAR/Fire", CHAN_6)
			TNT1 A 0	
			Goto FireBullet
			
		FireSoundChanZoomed:		
			TNT1 A 0 A_JumpIfInventory("HARFireSound",1,3)
			TNT1 A 0 A_GiveInventory("HARFireSound", 1)
			TNT1 A 0 A_PlaySound("HAR/AltFire", CHAN_5)
			TNT1 A 0 A_Jump(256, 2)
			TNT1 A 0 A_TakeInventory("HARFireSound", 1)
			TNT1 A 0 A_PlaySound("HAR/AltFire", CHAN_6)
			TNT1 A 0	
			Goto FireBulletZoomed
		// ------------------------------------------------

	FireBullet:
		TNT1 A 0 A_FireCustomMissile("DecorativeTracer", random(-2,2),0,12,-6,0,random(-1,1))
		TNT1 A 0 A_FireBullets (2, 2, 1, 10, "MachinegunBulletPuff")
		TNT1 A 0 A_TakeInventory("D4Bullets", 1)
		TNT1 A 0 A_SetBlend("orange",0.065,6)
		TNT1 A 1
        TNT1 A 0 A_SetPitch(-1.3 + pitch)
		TNT1 A 1 
		TNT1 A 1 A_FireCustomMissile("RifleCaseSpawn",5,0,6,-14)
		TNT1 A 1 A_SetPitch(0.6 + pitch)
		Goto Flash
		
		FireBulletZoomed:
			TNT1 A 0 A_FireCustomMissile("DecorativeTracer", 0,0,0,-6,0,0)
			TNT1 A 0 A_FireBullets (0.0, 0.0, 1, 14, "MachinegunBulletPuff")
			TNT1 A 0 A_RailAttack(14,0,0,"none","none",RGF_SILENT,0,"MachineGunBulletPuff")
			TNT1 A 0 A_TakeInventory("D4Bullets", 2)
			TNT1 A 0 A_SetBlend("orange",0.05,6)
			TNT1 A 1
			TNT1 A 1 A_SetPitch(-1.3 + pitch)
			TNT1 A 1 A_FireCustomMissile("RifleCaseSpawn",5,0,6,-14)
			TNT1 A 1 A_FireCustomMissile("RifleCaseSpawn",5,0,6,-14)
			TNT1 A 1
			TNT1 A 1 A_SetPitch(0.5 + pitch)
			Goto Flash
			
		FireMicroMissile:
			TNT1 A 0 A_SetBlend("orange",0.07,7)
			TNT1 A 0 A_TakeInventory("D4Bullets", 3)
			TNT1 A 0 A_FireCustomMissile("HARRocket", random(-2,2),0,9,-6,0,random(-1,1))
			TNT1 A 0 A_GiveInventory("FiredMissile", 1)
			TNT1 A 2 A_SetPitch(-2.0 + pitch)
			TNT1 A 1
			TNT1 AA 1 A_SetPitch(1.0 + pitch)
			Goto Flash
		
// ------------------------------------------------------------------------------------
//
// Alt-Fire
//
// ------------------------------------------------------------------------------------
		
	AltFire:
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive", 1, "Aim")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive", 1, "OpenHatch")
		Goto NoUpgrades
		
	OpenHatch:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("HAROpenHatch", 1, "CloseHatch")
		TNT1 A 0 A_PlaySound("HAR/OpenHatch", 1)
		M3GA ABCDEF 1
		TNT1 A 0 A_PlaySound("HAR/MicroReady", 4)
		M3GB A 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_GiveInventory("HAROpenHatch", 1)
		TNT1 A 0 A_TakeInventory("FiredMissile", 6)
		Goto WReady
			
	FireMicro:		
		"####" "#" 0 A_JumpIfInventory("D4Bullets", 3, 1)
		Goto DryFire
		TNT1 A 0 A_GunFlash("FirePrimary")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("FiredMissile", 5, "FireMicro6")
		TNT1 A 0 A_JumpIfInventory("FiredMissile", 4, "FireMicro5")
		TNT1 A 0 A_JumpIfInventory("FiredMissile", 3, "FireMicro4")
		TNT1 A 0 A_JumpIfInventory("FiredMissile", 2, "FireMicro3")
		TNT1 A 0 A_JumpIfInventory("FiredMissile", 1, "FireMicro2")
		Goto FireMicro1
		
		FireMicro1:
			TNT1 A 0 A_PlaySound("HAR/Missile", 7)
			M3GB B 1 Bright Offset(1,33)
			M3GB C 1 Bright Offset(3,35) 
			M3GB D 1 Offset(1,33) 
			M3GB D 1 Offset(1,32) 
			M3GB D 1
			Goto FireEndMicro	
		FireMicro2:
			TNT1 A 0 A_PlaySound("HAR/Missile", 6)
			M3GB E 1 Bright Offset(1,33)
			M3GB F 1 Bright Offset(3,35) 
			TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
			M3GB G 1 Offset(1,33)
			M3GB G 1 Offset(1,32)
			M3GB G 1
			Goto FireEndMicro
		FireMicro3:
			TNT1 A 0 A_PlaySound("HAR/Missile", 5)
			M3GB H 1 Bright Offset(1,33)
			M3GB I 1 Bright Offset(3,35) 
			TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
			M3GB J 1 Offset(1,33)
			M3GB J 1 Offset(1,32)
			M3GB J 1
			Goto FireEndMicro
		FireMicro4:
			TNT1 A 0 A_PlaySound("HAR/Missile", 4)
			M3GB K 1 Bright Offset(1,33)
			M3GB L 1 Bright Offset(3,35) 
			TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
			M3GB M 1 Offset(1,33)
			M3GB M 1 Offset(1,32)
			M3GB M 1
			Goto FireEndMicro
		FireMicro5:
			TNT1 A 0 A_PlaySound("HAR/Missile")
			M3GB N 1 Bright Offset(1,33)
			M3GB O 1 Bright Offset(3,35) 
			TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
			M3GB P 1 Offset(1,33)
			M3GB P 1 Offset(1,32)
			M3GB P 1
			Goto FireEndMicro	
		FireMicro6:
			TNT1 A 0 A_PlaySound("HAR/Missile", 7)
			M3GB Q 1 Bright Offset(1,33)
			M3GB R 1 Bright Offset(3,35) 
			TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
			M3GA F 1 Offset(1,33)
			M3GA F 1 Offset(1,32)
			M3GA F 1
			Goto FireEndMicro
			
	FireEndMicro:
		"####" "#" 0 A_JumpIfInventory("FiredMissile",6,3)
		"####" "#" 0 A_Refire("FireMicro")
        "####" "#" 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		TNT1 A 0 A_PlaySound("HAR/CloseHatch", 4)
		M3GA ECA 1
		TNT1 A 0 A_TakeInventory("HAROpenHatch", 1)
		TNT1 A 0 A_TakeInventory("FiredMissile", 6)
		TNT1 A 0 A_Refire("Fire")
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady		
		
	CloseHatch:
		TNT1 A 0 A_TakeInventory("FiredMissile", 6)
		TNT1 A 0 A_PlaySound("HAR/CloseHatch", 4)
		M3GA EDCBA 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_TakeInventory("HAROpenHatch", 1)
		Goto WReady
		
	Aim:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,"NoAim")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_Giveinventory("Zoomed",1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
        TNT1 A 0 A_ZoomFactor(2.0)
		TNT1 A 0 A_Giveinventory("ADSmode",1)
        M2GA AB 1 BRIGHT
		R016 M 2
        Goto ReadyZoomed

		FireZoomed:  // This part is not hastened
			"####" "#" 0 A_JumpIfInventory("D4Bullets", 2, 1)
			Goto DryFire
			"####" "#"0 A_GunFlash("FirePrimary")
			"####" "#" 1 BRIGHT
			"####" "#" 1 BRIGHT
			"####" "#" 1 BRIGHT
			"####" "#" 1 BRIGHT
			"####" "#" 1 BRIGHT
			"####" "#" 1 BRIGHT
			Goto FinishFireRelease

	NoAim:
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
		R016 M 2 BRIGHT
        M2GA BA 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		Goto WReady
		
	NoAimFast:
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
        R016 M 1 BRIGHT
        M2GA A 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		Goto WReady

	Spawn:
		D4W4 A 1
		TNT1 A 0 A_SpawnItemEx("D4HARSpawnerNoAmmo", 0, 0, 0, velx, vely, velz, angle, SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY)
		Stop
		
// ------------------------------------------------------------------------------------
//
// Dual Wield States
//
// ------------------------------------------------------------------------------------

	// Basic States
	ReadyDualWield:
		M1GG A 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto WReady		

	RightWeapon:
		M12G A 1 A_JumpIfInventory("FiredSecondary", 1, "FireSecondary")
		Goto Flash
		
	DualWieldAnimChecker:
		TNT1 A 0 A_JumpIfInventory("HARDualWield", 1, 4)
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"SwapMod")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		Goto WReady
		TNT1 A 0 A_Takeinventory("Taunting",1)
		TNT1 A 0 A_Takeinventory("Reloading",1)
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKickDualWield")
		Goto WReady
	DeployDualWield:
		TNT1 A 0 A_GunFlash("FakeSelect")
		M1GS DCBA 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
	FakeSelect:
		M12S DCBA 1
		Goto Flash		
	FakeDeselect:
		M12S ABCD 1
		Goto Flash
		
	//Dual Wield Switcher
	DualWield:
		TNT1 A 0 A_Takeinventory("StartDualWield",1)
		TNT1 A 0 A_JumpIfInventory("HARDualWieldEnabler", 1, "GetDual")
		Goto FailDualWield
		
	GetDual:	
        TNT1 A 0 A_GiveInventory("NoParkour",1)
		TNT1 A 0 A_JumpIfInventory("HARDualWield", 1, "UnDual")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"DualWieldMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"DualWieldScope")
		MAGS A 0
		Goto DualWieldProcced		
		DualWieldScope:
			M2GS A 0
			Goto DualWieldProcced	
		DualWieldMissile:
			TNT1 A 0 A_TakeInventory("HAROpenHatch", 1)
			TNT1 A 0 A_TakeInventory("FiredMissile", 6)
			M3GS A 0
			Goto DualWieldProcced		
	DualWieldProcced:
		"####" EDCBA 1
		TNT1 A 9
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_GiveInventory("HARDualWield", 1)
		TNT1 A 0 A_GiveInventory("IsDualWeapon",1)
		TNT1 A 0 A_GunFlash("FakeSelect")
		M1GS DCBA 1
        TNT1 A 0 A_TakeInventory("NoParkour",1)
		Goto WReady
		
	UnDual:
		TNT1 A 0 A_GunFlash("FakeDeselect")
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_TakeInventory("HARDualWield", 1)
        TNT1 A 0 A_TakeInventory("IsDualWeapon",1)
		M1GS ABCD 1
		TNT1 A 9
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"UnDualMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"UnDualScope")
		MAGS A 0
		Goto UnDualProcced		
		UnDualScope:
			M2GS A 0
			Goto UnDualProcced	
		UnDualMissile:
			M3GS A 0
			Goto UnDualProcced			
	UnDualProcced:
		"####" ABCDE 1
        TNT1 A 0 A_TakeInventory("NoParkour",1)
		Goto WReady
		
	//Fire States
	FireDualWield:
		M1GG A 0 A_JumpIfInventory("D4Bullets",1,1)
		Goto DryFire
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("HARFireSound",1,3)
		TNT1 A 0 A_GiveInventory("HARFireSound", 1)
		TNT1 A 0 A_PlaySound("HAR/Fire", CHAN_5)
		TNT1 A 0 A_Jump(256, 2)
		TNT1 A 0 A_TakeInventory("HARFireSound", 1)
		TNT1 A 0 A_PlaySound("HAR/Fire", CHAN_6)	
		TNT1 A 0 A_FireCustomMissile("DecorativeTracer", random(-4,4),0,12,-6,0,random(-1,1))
		TNT1 A 0 A_FireBullets (4, 4, 1, 10, "MachinegunBulletPuff")
		TNT1 A 0 A_TakeInventory("D4Bullets", 1)
		TNT1 A 0 A_SetBlend("orange",0.065,6)
		M1GF A 1 BRIGHT 
		M1GF B 1 BRIGHT A_SetPitch(-1.3 + pitch)
		M1GF C 1 BRIGHT A_FireCustomMissile("RifleCaseSpawn",5,0,6,-14)
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
		M1GF D 1 BRIGHT
		M1GF E 1
		M1GG A 1 A_Refire("Fire")
        M1GG A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FireSecondary:
		M12G A 0 A_JumpIfInventory("D4Bullets",1,1)
		Goto DryFireDual
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GiveInventory("ChecksIsFiringTheRightGun",1)
		TNT1 A 0 A_JumpIfInventory("LeftHARSound",1,3)
		TNT1 A 0 A_GiveInventory("LeftHARSound", 1)
		TNT1 A 0 A_PlaySound("HAR/Fire", 4)
		TNT1 A 0 A_Jump(256, 2)
		TNT1 A 0 A_TakeInventory("LeftHARSound", 1)
		TNT1 A 0 A_PlaySound("HAR/Fire", 7)
		TNT1 A 0 A_FireCustomMissile("DecorativeTracer", random(-4,4),0,12,-6,0,random(-1,1))
		TNT1 A 0 A_FireBullets (4, 4, 1, 10, "MachinegunBulletPuff")
		TNT1 A 0 A_TakeInventory("D4Bullets", 1)
		TNT1 A 0 A_SetBlend("orange",0.065,6)
		M12F A 1 BRIGHT 
		M12F B 1 BRIGHT A_SetPitch(-1.3 + pitch)
		M12F C 1 BRIGHT A_FireCustomMissile("RifleCaseSpawn",5,0,-6,-14)
		M12F D 1 BRIGHT
		TNT1 A 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,2)
		M12F E 1
		M12G A 1 A_JumpIfInventory("FiredSecondary", 1, "FireSecondary")
		TNT1 A 0 A_TakeInventory("ChecksIsFiringTheRightGun",1)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto Flash
		
	DryFireDual:
		TNT1 A 0 A_TakeInventory("ChecksIsFiringTheRightGun",1)
		M12G A 1 A_PlaySound("weapons/empty")
		Goto Flash
		
// ------------------------------------------------------------------------------------
//
// Mod Swaping
//
// ------------------------------------------------------------------------------------

	StartUpgrader:
		TNT1 A 0 A_JumpIfInventory("HAROpenHatch",1,1)
		Goto UpgraderCheckZoomed
		TNT1 A 0 A_PlaySound("MICROEND", 4)
		M3GA ECA 1
		TNT1 A 0 A_TakeInventory("FiredMissile", 6)
		TNT1 A 0 A_TakeInventory("HAROpenHatch", 1)
		Goto ContinueForUpgrader

	UpgraderCheckZoomed:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,1)
		Goto ContinueForUpgrader
        TNT1 A 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_Takeinventory("ADSmode",1)
        R016 M 1 BRIGHT
        M2GA BA 1
		TNT1 A 0 A_Takeinventory("Zoomed",1)
		Goto ContinueForUpgrader

	ContinueForUpgrader:
		TNT1 A 0 A_TakeInventory("StartUpgrader",1)
		TNT1 A 0 A_GiveInventory("UpgradeHAR",1)
		TNT1 A 0 A_SelectWeapon("D4WeaponUpgrader")
		Goto WReady

	SwapMod:
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory("ModHasTacticalScope", 1, "ToTacticalScope")
		TNT1 A 0 A_JumpIfInventory("ModHasMicroMissile", 1, "ToMicroMissiles")
		Goto NoUpgrades
		
		ToTacticalScope:
			TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"FromMicroMissilesToTacticalScope")
			TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"TacticalScopeToNextCheck") 
			Goto FromNormalToSwap
			
			TacticalScopeToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasMicroMissile",1,"FromTacticalScopeToMicroMissiles")
				Goto NoOtherMod
			
		ToMicroMissiles:
			TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"FromTacticalScopeToMicroMissiles")
			TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"MicroMissilesToNextCheck") 
			Goto FromNormalToSwap
			
			MicroMissilesToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasTacticalScope",1,"FromMicroMissilesToTacticalScope")
				Goto NoOtherMod
				
	FromNormalToSwap:	
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		MAGG A 1 Offset(4,52)
		MAGG A 1 Offset(8,72)
		MAGG A 1 Offset(12,92)
		MAGG A 1 Offset(16,112)
		TNT1 A 5
		TNT1 A 0 A_JumpIfInventory("ModHasTacticalScope", 1, "SwapTacticalScope")
		TNT1 A 0 A_JumpIfInventory("ModHasMicroMissile", 1, "SwapMicroMissiles")
		Goto NoOtherMod
		
	FromTacticalScopeToMicroMissiles:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		//hide TacticalScope
		M2GG A 1 Offset(4,52)
		M2GG A 1 Offset(8,72)
		M2GG A 1 Offset(12,92)
		M2GG A 1 Offset(16,112)
		TNT1 A 5
		TNT1 A 0 A_TakeInventory("ModTacticalScopeActive", 1)
	SwapMicroMissiles:
		TNT1 A 0 A_GiveInventory("ModMicroMissileActive", 1)
		//pull of and arm MicroMissiles mod
		M3GU A 1 Offset(16,97)
		M3GU A 1 Offset(16,82)
		TNT1 A 0 A_Playsound("Har/Micro/Equip",5,1)
		M3GU B 1 Offset(16,78)
		M3GU C 1 Offset(16,74)
		M3GU D 1 Offset(16,70)
		M3GU E 1 Offset(16,66)
		M3GU F 1 Offset(16,62)
		M3GU G 1 Offset(16,58)
		M3GU H 1 Offset(16,54)
		M3GU I 1 Offset(16,50)
		M3GU J 1 Offset(16,46)
		//attach mod
		M3GU K 1 Offset(18,48)
		M3GU K 1 Offset(20,50)
		M3GU K 1 Offset(22,52)
		//bounce back
		M3GU K 1 Offset(20,50)
		M3GU K 1 Offset(18,48)
		M3GU K 1 Offset(16,46)
		//Finishes offset restoration
		M3GU L 1 Offset(12,44)
		M3GU M 1 Offset(8,42) 
		M3GU N 1 Offset(4,39)
		M3GU N 1 Offset(1,36)
		M3GG A 1 Offset(1,33)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FromMicroMissilesToTacticalScope:	
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		M3GG A 1 Offset(4,52)
		M3GG A 1 Offset(8,72)
		M3GG A 1 Offset(12,92)
		M3GG A 1 Offset(16,112)
		TNT1 A 5
		TNT1 A 0 A_TakeInventory("ModMicroMissileActive", 1)
	SwapTacticalScope:
		TNT1 A 0 A_GiveInventory("ModTacticalScopeActive", 1)
		M2GU A 1 Offset(16,102)
		M2GU A 1 Offset(16,92)
		M2GU F 0 A_PlaySound("Har/Scope/Equip",5,1)
		M2GU A 1 Offset(16,82)
		M2GU B 1 Offset(16,72)
		M2GU C 1 Offset(16,62)
		M2GU D 1 Offset(16,52)
		M2GU EF 1 
		M2GU GGGHIII 2
		M2GU IJK 1
		M2GU L 1 Offset(12,47)
		M2GU M 1 Offset(8,42)
		M2GU N 1 Offset(4,37)
		M2GU O 1 Offset(1,32)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady

// ------------------------------------------------------------------------------------
//
// Flash States, Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------
	
	PunchFlash:
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"PunchFlashDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"PunchFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"PunchFlashScope")
		MAGK A 0
		Goto RealPunchFlash

		PunchFlashScope:
			M2GK A 0
			Goto RealPunchFlash
		PunchFlashMicroMissile:
			M3GK A 0
			Goto RealPunchFlash
			RealPunchFlash:
				"####" ACE 1
				Goto Flash

	PunchFlashDualWield:
		M1GS ABCD 1
		Stop
	
	KickFlash:
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"KickFlashDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"KickFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"KickFlashScope")
		MAGK A 0
		Goto RealKickFlash
		
		KickFlashScope:
			M2GK A 0
			Goto RealKickFlash
		KickFlashMicroMissile:
			M3GK A 0
			Goto RealKickFlash	
		KickFlashDualWield:
			M1GK A 0
			Goto RealKickFlash
			RealKickFlash:
				"####" ABCD 1
				"####" E 8
				"####" DCBA 1
				Goto Flash

	AirKickFlash:
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"AirKickFlashDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"AirKickFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"AirKickFlashScope")
		MAGK A 0
		Goto RealAirKickFlash

		AirKickFlashScope:
			M2GK A 0
			Goto RealAirKickFlash
		AirKickFlashMicroMissile:
			M3GK A 0
			Goto RealAirKickFlash
		AirKickFlashDualWield:
			M1GK A 0
			Goto RealAirKickFlash
			RealAirKickFlash:
				"####" ABCD 1
				"####" E 10
				"####" DCBA 1
				Goto Flash
		
	SlideKickStartFlash:
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"SlideKickStartFlashDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"SlideKickStartFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"SlideKickStartFlashScope")
		MAGS A 0
		Goto RealSlideKickStartFlash
		
		SlideKickStartFlashScope:
			M2GS A 0
			Goto RealSlideKickStartFlash	
		SlideKickStartFlashMicroMissile:
			M3GS A 0
			Goto RealSlideKickStartFlash
			RealSlideKickStartFlash:
				"####" EDCB 1
				"####" A 17
				Goto SlideKickEndFlash
		
	SlideKickStartFlashDualWield:
		M1GK ABCD 1
		M1GK E 17
		Goto SlideKickEndFlash
				
	SlideKickEndFlash:
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"SlideKickEndFlashDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"SlideKickEndFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"SlideKickEndFlashScope")
		MAGS A 0
		Goto RealSlideKickEndFlash
		
		SlideKickEndFlashScope:
			M2GS A 0
			Goto RealSlideKickEndFlash
		SlideKickEndFlashMicroMissile:
			M3GS A 0
			Goto RealSlideKickEndFlash
			RealSlideKickEndFlash:
				"####" A 5
				"####" BCDE 1
				Goto Flash

	SlideKickEndFlashDualWield:
		M1GK E 5
		M1GK DCBA 1
		Goto Flash

	TauntFlash:
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"TauntFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"TauntFlashScope")
		MAGK A 0
		Goto RealTauntFlash

		TauntFlashScope:
			M2GK A 0
			Goto RealTauntFlash
		TauntFlashMicroMissile:
			M3GK A 0
			Goto RealTauntFlash
			
			RealTauntFlash:
				"####" ABCD 1
				"####" E 35
				"####" DCBA 1
				Goto Flash
		
	GrenadeFlash:
		TNT1 A 0 A_JumpIfInventory("HARDualWield",1,"GrenadeFlashDualWield")
		TNT1 A 0 A_JumpIfInventory("ModMicroMissileActive",1,"GrenadeFlashMicroMissile")
		TNT1 A 0 A_JumpIfInventory("ModTacticalScopeActive",1,"GrenadeFlashScope")
		MAGK A 0
		Goto RealGrenadeFlash
		
		GrenadeFlashScope:
			M2GK A 0
			Goto RealGrenadeFlash		
		GrenadeFlashMicroMissile:
			M3GK A 0
			Goto RealGrenadeFlash		
		GrenadeFlashDualWield:
			M1GK A 0
			Goto RealGrenadeFlash
			RealGrenadeFlash:
				"####" ABCDE 1
				"####" "#" 0 A_JumpIfInventory("HARDualWield",1,"UnFlash")
				Goto Flash
	}
}

ACTOR HARRocket: revenantseekermissiles
{
	Radius 8
	Height 8
	Damage 12
	Scale 0.5
	Speed 45
	-NOEXTREMEDEATH
	-GHOST
	-THRUGHOST
	+SEEKERMISSILE
	seesound "None"
	deathsound "HAR/Micro/Explosion"
	Damagetype "ExplosiveImpact"
	Species "Marines"
	States
	{
	Spawn:
	    MISL A 1 Bright 
		TNT1 A 0 A_SpawnItem("RedFlareSmall",-20,0)
		TNT1 A 0 A_CustomMissile ("MeleeSmoke", 2, 0, random (160, 210), 2, random (-30, 30))
		TNT1 A 0 ThrustThingZ(0,random(-3, 2),0,1)
		MISL A 1 Bright 
		TNT1 A 0 A_SpawnItem("RedFlareSmall",-20,0)
		TNT1 A 0 A_CustomMissile ("MeleeSmoke", 2, 0, random (160, 210), 2, random (-30, 30))
		MISL A 1 Bright 
		TNT1 A 0 A_SpawnItem("RedFlareSmall",-20,0)
		TNT1 A 0 A_CustomMissile ("MeleeSmoke", 2, 0, random (160, 210), 2, random (-30, 30))
		TNT1 A 0 ThrustThingZ(0,random(-3, 2),0,1)
		MISL A 1 Bright 
		TNT1 A 0 A_SpawnItem("RedFlareSmall",-20,0)
		TNT1 A 0 A_CustomMissile ("MeleeSmoke", 2, 0, random (160, 210), 2, random (-30, 30))
		TNT1 A 0 ThrustThing(angle*256/360+64,random(-1, 1),1, 0)
		Loop
	}
}

Actor FiredMissile : Inventory {inventory.maxamount 6}
Actor HAROpenHatch : Inventory {inventory.maxamount 1}
Actor HARDualWield : Inventory {inventory.maxamount 1}
Actor HARFireSound : Inventory {inventory.maxamount 1}
Actor LeftHARSound : Inventory {inventory.maxamount 1}
Actor HARDualWieldEnabler : Inventory {inventory.maxamount 1}