ACTOR D4Shotgun : D4WeaponBase replaces Shot_Gun
{
	Weapon.SelectionOrder 1300
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 2
	Weapon.AmmoType1 "D4Shells"
	Weapon.AmmoUse2 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType1 "D4Shells"
    Inventory.PickupSound "weapons/sgpump"
	Inventory.PickupMessage "You got the Combat Shotgun!"
	Obituary "%o was shot down by %k's Combat Shotgun"
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	+WEAPON.NOALERT
	Tag "Combat Shotgun"
	States
	{		
	WReady:
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
        TNT1 A 0 A_JumpIfInventory("Reloading",1,"SwapMod")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"StartUpgrader")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"ReadyExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"ReadyTriShot")
		41SG A 1 A_WeaponReady
		Loop
		
		ReadyTriShot:
			D4SG A 1 A_WeaponReady
			Goto WReady		
		ReadyExplosive:
			42SG A 1 A_WeaponReady
			Goto WReady
		
	Deselect:
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"DeselectExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"DeselectTriShot")
		41SS A 0
		Goto RealDeselect
		
		DeselectTriShot:
			D4SS A 0
			Goto RealDeselect		
		DeselectExplosive:
			42SS A 0
			Goto RealDeselect
		
	RealDeselect:
		"####" EDCBA 1
		"####" A 0
		Goto DeselectSequence
				
	Select:
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("ShotgunSelected",1)
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon", 1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"StartSelectExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"StartSelectTriShot")
		41SS A 0
		Goto SelectSequence
		StartSelectTriShot:
			D4SS A 0
			Goto SelectSequence
		StartSelectExplosive:
			42SS A 0
			Goto SelectSequence
		
	Ready:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("Weapon/Switch")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"SelectExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"SelectTriShot")
		41SS A 0
		Goto RealSelect
		
		SelectTriShot:
			D4SS A 0
			Goto RealSelect
		SelectExplosive:
			42SS A 0
			Goto RealSelect
		
	RealSelect:
		"####" ABCDE 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	Spawn:
		D4W3 A -1
		Stop

// ------------------------------------------------------------------------------------
//
// Fire Related
//
// ------------------------------------------------------------------------------------

	NoAmmo:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive", 1, "NoAmmoExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"NoAmmoTriShot")
		41SG A 0
		Goto DryFire
		
		NoAmmoTriShot:
			D4SG A 0
			Goto DryFire
		NoAmmoExplosive:
			42SG A 0
			Goto DryFire
		
	DryFire:
		"####" A 1 A_PlaySound("weapons/empty")
		Goto WReady
		
	Fire:
		TNT1 A 0 A_JumpIfInventory("ShotgunTriShotReady", 1, "TriShotFire")
		TNT1 A 0 A_JumpIfInventory("ShotgunExplosiveReady", 1, "ExplosiveFire")
		TNT1 A 0 A_JumpIfInventory("D4Shells", 1, 1)
		Goto NoAmmo
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_GunFlash("FirePrimary")
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive", 1, "FireExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"FireTriShot")
		41SF A 0
		Goto FireRelease		
		
		FireTriShot:
			D4SF A 0
			Goto FireRelease
		FireExplosive:
			42SF A 0
			Goto FireRelease		
			
	FireRelease:
        "####" A 1 BRIGHT Offset(15,40)
		"####" B 1 BRIGHT Offset(30,55)
		"####" CC 4
		"####" C 1 Offset(24,50)
		"####" C 1 Offset(18,46)
		"####" C 1 Offset(13,42)
		"####" C 1 Offset(8,38)
		"####" C 2 Offset(3,35)
		"####" C 1 Offset(1,32)
	Pump:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive", 1, "PumpExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"PumpTriShot")
		41SP A 0
		Goto PumpRelease
		
		PumpTriShot:
			D4SP A 0
			Goto PumpRelease
		PumpExplosive:
			42SP A 0
			Goto PumpRelease		
			
	PumpRelease:
		"####" "#" 0 A_JumpIfInventory("PowerDoubleFiringSpeed",1,"PumpHaste")
		"####" A 1 Offset(3,34)
		"####" A 0 A_PlaySound("CombatShotgun/Pump", 3)
		"####" C 1 Offset(6,36)
		"####" E 1 Offset(9,38)
		"####" E 1 Offset(12,41)
		"####" A 0 A_FireCustomMissile ("ShotCaseSpawn", 0, 0, 20, -8)
		"####" E 4 Offset(12,41)
		"####" E 1 Offset(9,39)
		"####" D 1 Offset(7,37)
		"####" C 1 Offset(5,36)
		"####" B 1 Offset(3,34)
		"####" A 1 Offset(1,32)
		Goto FinishFire
			
	PumpHaste:
		"####" A 1 Offset(3,34)
		"####" A 0 A_PlaySound("CombatShotgun/Pump", 3)
		"####" E 1 Offset(9,38)
		"####" A 0 A_FireCustomMissile ("ShotCaseSpawn", 0, 0, 20, -8)
		"####" E 1 Offset(12,41)
		"####" E 1 Offset(12,41)
		"####" E 1 Offset(9,39)
		"####" D 1 Offset(7,37)
		"####" B 1 Offset(3,34)
		"####" A 1 Offset(1,32)
		Goto FinishFire
		
	FinishFire:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive", 1, "FinishFireExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"FinishFireTriShot")
		41SG A 0
		Goto FinishFireRelease
		
		FinishFireTrishot:
			D4SG A 0
			Goto FinishFireRelease
		FinishFireExplosive:
			42SG A 0
			Goto FinishFireRelease
		
	FinishFireRelease:
		"####" A 4 A_WeaponReady
		"####" A 0 A_Refire
        "####" A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FirePrimary:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.97,ZOOM_NOSCALETURNING)
		RIFF A 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 12, -12, 0, random(-1,1))
		TNT1 AA 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 AAAAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, -1, 0, 0, random(-9,9))
		TNT1 AAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, -1, 0, 0, random(-9,9))
		TNT1 AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, -12, 0, random(-3,3))
		TNT1 A 0 A_FireBullets (2.5, 2.5, 3, 13, "ShotgunPuff", FBF_NORANDOM)
		TNT1 A 0 A_FireBullets (2.5, 2.5, 6, 13, "ShotgunPuff2", FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("CombatShotgun/Fire", 1)
		TNT1 A 0 A_SetBlend("orange",0.1,8)
		TNT1 A 0 A_TakeInventory("D4Shells", 1)
		TNT1 A 1
		TNT1 A 0 A_ZoomFactor(1,ZOOM_NOSCALETURNING)
		TNT1 A 1 A_SetPitch(-6.0 + pitch)
		TNT1 AAAAAA 1 A_SetPitch(+1.0 + pitch)
		Goto Flash
		
	PrimaryTriShot:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_TakeInventory("D4Shells", 1)
		TNT1 A 0 A_FireCustomMissile("DistantFireSoundShotgun", random(-1,1), 0, 12, -12, 0, random(-1,1))
		TNT1 AA 0 A_FireCustomMissile("YellowFlareSpawn",0,0,0,0)
		TNT1 A 0 A_SpawnItemEx("PlayerMuzzle1",30,5,30)
		TNT1 A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
		TNT1 AAAAAAAAAAAAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-12,12), 0, -1, 0, 0, random(-9,9))
		TNT1 AAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles2", random(-12,12), 0, -1, 0, 0, random(-9,9))
		TNT1 AAAAA 0 A_FireCustomMissile("DecorativeTracer", random(-3,3), 0, 0, -12, 0, random(-3,3))
		TNT1 A 0 A_FireBullets (2.5, 2.5, 3, 13, "ShotgunPuff", FBF_NORANDOM)
		TNT1 A 0 A_FireBullets (2.5, 2.5, 6, 13, "ShotgunPuff2", FBF_NORANDOM)
		TNT1 A 0 A_PlaySound("CombatShotgun/Charge/Fire", 1)
		TNT1 A 0 A_ZoomFactor(0.97,ZOOM_NOSCALETURNING)
		TNT1 A 0 A_SetBlend("orange",0.1,8)
		TNT1 A 0 A_Recoil(1)
		TNT1 A 0 A_SetPitch(pitch-2.8)
		TNT1 A 1 BRIGHT
		TNT1 A 0 A_ZoomFactor(1,ZOOM_NOSCALETURNING)
		TNT1 AAAA 1 A_SetPitch(pitch+0.7) 
		Goto Flash

// ------------------------------------------------------------------------------------
//
// Alt-Fire Related
//
// ------------------------------------------------------------------------------------
		
	AltFire:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive", 1, "ExplosiveMode")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive", 1, "TriShotMode")
		Goto NoUpgrades
		
	TriShotMode:
		TNT1 A 0 A_JumpIfInventory("SGTriShotTimer",1,"ModNotReady")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_playsound("CombatShotgun/Charge", 6)
		D4SG B 1 BRIGHT		
		D4SG C 1 BRIGHT
		D4SG D 1 BRIGHT
		TNT1 A 0 A_GiveInventory("ShotgunTriShotReady", 1)
	TriShotReady:
		D4SG D 1 BRIGHT A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY|WRF_NOSWITCH)
		TNT1 A 0 A_Refire("TriShotReady")
		TNT1 A 0 A_TakeInventory("ShotgunTriShotReady", 1)
		D4SG DCB 1 BRIGHT
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	TriShotFire:
		TNT1 A 0 A_TakeInventory("ShotgunTriShotReady", 1)
		TNT1 A 0 A_JumpIfInventory("D4Shells", 3, 1)
		Goto NoAmmo
		D4SA A 1 BRIGHT Offset(8,38) A_GunFlash("PrimaryTriShot")
		D4SA B 1 BRIGHT Offset(16,44)
		D4SG C 1 Offset(13,42)
		D4SG C 1 Offset(10,40)
		D4SG C 1 Offset(7,38)
		D4SA C 1 BRIGHT Offset(15,44) A_GunFlash("PrimaryTriShot")
		D4SA D 1 BRIGHT Offset(23,50)
		D4SG B 1 Offset(13,42)
		D4SG B 1 Offset(10,40)
		D4SG B 1 Offset(7,38)
		TNT1 A 0 A_GiveInventory("SGTriShotTimer",52)
		D4SA E 1 BRIGHT Offset(15,44) A_GunFlash("PrimaryTriShot")
		D4SA F 1 BRIGHT Offset(23,50) ACS_NamedExecuteAlways("ShotgunTriShotTimer",0,0,0,0)
		D4SG A 1 Offset(25,52)
		D4SG A 1 Offset(20,48)
		D4SG A 1 Offset(15,44)
		D4SG A 1 Offset(10,40)
		D4SG A 1 Offset(5,36)
		D4SG A 1 Offset(1,32)
		Goto Pump
		
	ExplosiveMode:
		TNT1 A 0 A_JumpIfInventory("SGGrenadeTimer",1,"ModNotReady")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		42SG B 1
		TNT1 A 0 A_GiveInventory("ShotgunExplosiveReady", 1)
	ExplosiveReady:
		42SG B 1 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY|WRF_NOSWITCH)
		TNT1 A 0 A_Refire("ExplosiveReady")
		TNT1 A 0 A_TakeInventory("ShotgunExplosiveReady", 1)
		42SG A 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady

	ExplosiveFire:
		TNT1 A 0 A_TakeInventory("ShotgunExplosiveReady", 1)
		TNT1 A 0 A_JumpIfInventory("D4Shells", 2, 1)
		Goto NoAmmo
		TNT1 A 0 A_FireCustomMissile("ShotgunGrenade", random(-1,1),0,6,-2,0,random(-1,1))
		TNT1 A 0 A_TakeInventory("D4Shells", 2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ZoomFactor(0.97,ZOOM_NOSCALETURNING)
		TNT1 A 0 A_PlaySound("CombatShotgun/Grenade", 1)
		TNT1 A 0 A_SetBlend("orange",0.12,10)
		TNT1 A 0 A_SetPitch(pitch-3)
		TNT1 A 0 ACS_NamedExecuteAlways("ShotgunGrenadeTimer",0,0,0,0)
		TNT1 A 0 A_GiveInventory("SGGrenadeTimer",52)
        42SF A 1 BRIGHT Offset(15,40)
        42SF B 1 BRIGHT Offset(30,55) A_ZoomFactor(1,ZOOM_NOSCALETURNING)
		42SG AAAA 1 A_SetPitch(pitch+0.5)
		42SG A 1 Offset(24,50) A_SetPitch(pitch+0.5)
		42SG A 1 Offset(18,46) A_SetPitch(pitch+0.5)
		42SG A 1 Offset(13,42)
		42SG A 1 Offset(8,38)
		42SG A 1 Offset(4,36)
		42SG A 1 Offset(2,34)
		42SG A 1 Offset(1,32)
		Goto Pump

// ------------------------------------------------------------------------------------
//
// Mod Swaping
//
// ------------------------------------------------------------------------------------
				
	StartUpgrader:
		TNT1 A 0 A_TakeInventory("StartUpgrader",1)
		TNT1 A 0 A_GiveInventory("UpgradeShotgun",1)
		TNT1 A 0 A_SelectWeapon("D4WeaponUpgrader")
		Goto WReady
		
	SwapMod:
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		TNT1 A 0 A_JumpIfInventory("ModHasTriShot", 1, "ToTriShot")
		TNT1 A 0 A_JumpIfInventory("ModHasExplosiveShot", 1, "ToGrenade")
		Goto NoUpgrades
		
		ToTrishot:
			TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"FromGrenadeToTrishot")
			TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"TrishotToNextCheck") 
			Goto FromNormalToSwap
			
			TrishotToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasExplosiveShot",1,"FromTrishotToGrenade")
				Goto NoOtherMod
			
		ToGrenade:
			TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"FromTrishotToGrenade")
			TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"GrenadeToNextCheck") 
			Goto FromNormalToSwap
			
			GrenadeToNextCheck:
				TNT1 A 0 A_JumpIfInventory("ModHasTriShot",1,"FromGrenadeToTrishot")
				Goto NoOtherMod
				
	FromNormalToSwap:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		41SG A 1 Offset(0,52)
		41SG A 1 Offset(0,72)
		41SG A 1 Offset(0,92)
		41SG A 1 Offset(0,112)
		TNT1 A 5
		TNT1 A 0 A_JumpIfInventory("ModHasTriShot", 1, "SwapTriShot")
		TNT1 A 0 A_JumpIfInventory("ModHasExplosiveShot", 1, "SwapGrenade")
		Goto NoOtherMod
		
	FromTrishotToGrenade:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		D4SG A 1 Offset(0,52)
		D4SG A 1 Offset(0,72)
		D4SG A 1 Offset(0,92)
		D4SG A 1 Offset(0,112)
		TNT1 A 5
		TNT1 A 0 A_TakeInventory("ModTriShotActive", 1)
	SwapGrenade:
		TNT1 A 0 A_GiveInventory("ModExplosiveShotActive", 1)
		42ST A 1 Offset(0,102)
		42ST B 1 Offset(0,92)
		42ST C 1 Offset(0,82)
		42ST D 1 Offset(0,72)
		42ST E 1 Offset(0,62)
		42ST F 1 Offset(0,52)
		42ST G 1 Offset(0,42)
		TNT1 A 0 A_playsound("CombatShotgun/SwitchGrenade",4)
		42ST H 1 Offset(0,32) 
		42ST I 1
		42ST JKK 2
		42ST LMN 1
		42ST O 1 Offset(5,37)
		42ST O 1 Offset(10,42)
		42ST O 1 Offset(15,47)
		42ST O 3
		42ST P 1 Offset(10,42)
		42ST P 1 Offset(5,37)
		42ST P 1 Offset(1,32)
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	FromGrenadeToTrishot:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		42SG A 1 Offset(0,52)
		42SG A 1 Offset(0,72)
		42SG A 1 Offset(0,92)
		42SG A 1 Offset(0,112)
		TNT1 A 5
		TNT1 A 0 A_TakeInventory("ModExplosiveShotActive", 1)
	SwapTrishot:
		TNT1 A 0 A_GiveInventory("ModTriShotActive", 1)
		D4ST A 1 Offset(0,102)
		D4ST B 1 Offset(0,92)
		D4ST C 1 Offset(0,82)
		D4ST D 1 Offset(0,72)
		D4ST E 1 Offset(0,62)
		D4ST F 1 Offset(0,52)
		D4ST G 1 Offset(0,42)
		D4ST G 0 A_playsound("CombatShotgun/SwitchTriShot",4)
		D4ST G 1 Offset(0,32) 
		D4ST HIJKL 1
		D4ST M 1 Offset(3,35)
		D4ST M 1 Offset(6,38)
		D4ST M 1 Offset(9,41)
		D4ST M 1 Offset(12,44)
		D4ST M 1 Offset(9,41)
		D4ST M 1 Offset(6,38)
		D4ST M 1 Offset(3,35)
		D4ST M 1 Offset(1,32)
		D4ST NO 3
		D4SG A 3
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady

// ------------------------------------------------------------------------------------
//
// Flash States, Here lies new animations under kicks, taunt and throw grenade
//
// ------------------------------------------------------------------------------------

	PunchFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"PunchFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"PunchFlashTriShot")
		41SK A 0
		Goto RealPunchFlash

		PunchFlashTriShot:
			D4SK A 0
			Goto RealPunchFlash	
		PunchFlashExplosive:
			42SK A 0
			Goto RealPunchFlash
			RealPunchFlash:
				"####" ACE 1
				Goto Flash
		
	KickFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"KickFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"KickFlashTriShot")
		41SK A 0
		Goto RealKickFlash
		
		KickFlashTriShot:
			D4SK A 0
			Goto RealKickFlash		
		KickFlashExplosive:
			42SK A 0
			Goto RealKickFlash
			RealKickFlash:
				"####" ABCD 1
				"####" E 8
				"####" DCBA 1
				Goto Flash

	AirKickFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"AirKickFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"AirKickFlashTriShot")
		41SK A 0
		Goto RealAirKickFlash

		AirKickFlashTriShot:
			D4SK A 0
			Goto RealAirKickFlash
		AirKickFlashExplosive:
			42SK A 0
			Goto RealAirKickFlash
			RealAirKickFlash:
				"####" ABCD 1
				"####" E 10
				"####" DCBA 1
				Goto Flash
		
	SlideKickStartFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"SlideKickStartFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"SlideKickStartFlashTriShot")
		41SS A 0
		Goto RealSlideKickStartFlash
		
		SlideKickStartFlashTriShot:
			D4SS A 0
			Goto RealSlideKickStartFlash		
		SlideKickStartFlashExplosive:
			42SS A 0
			Goto RealSlideKickStartFlash
			RealSlideKickStartFlash:
				"####" EDCB 1
				"####" A 17
	SlideKickEndFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"SlideKickEndFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"SlideKickEndFlashTriShot")
		41SS A 0
		Goto RealSlideKickEndFlash
		
		SlideKickEndFlashTriShot:
			D4SS A 0
			Goto RealSlideKickEndFlash		
		SlideKickEndFlashExplosive:
			42SS A 0
			Goto RealSlideKickEndFlash
			RealSlideKickEndFlash:
				"####" A 5
				"####" BCDE 1
				Goto Flash

	TauntFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"TauntFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"TauntFlashTriShot")
		41SS A 0
		Goto RealTauntFlash

		TauntFlashTriShot:
			D4SS A 0
			Goto RealTauntFlash
		TauntFlashExplosive:
			42SS A 0
			Goto RealTauntFlash
			RealTauntFlash:
				"####" EDCB 1
				"####" A 35
				"####" BCDE 1
				Goto Flash
		
	GrenadeFlash:
		TNT1 A 0 A_JumpIfInventory("ModExplosiveShotActive",1,"GrenadeFlashExplosive")
		TNT1 A 0 A_JumpIfInventory("ModTriShotActive",1,"GrenadeFlashTriShot")
		41SK A 0
		Goto RealGrenadeFlash
		
		GrenadeFlashTriShot:
			D4SK A 0
			Goto RealGrenadeFlash	
		GrenadeFlashExplosive:
			42SK A 0
			Goto RealGrenadeFlash
			RealGrenadeFlash:
				"####" ABCDE 1
				Goto Flash
	}
}

ACTOR ShotgunGrenade: ShortGrenade
{
	Radius 4
	Height 4
	Speed 40
	Damage 6
    Gravity 0.4
	Scale 0.5
	Renderstyle Add
	States
	{
	Spawn:
	    TNT1 A 0 A_SpawnItem ("CoolAndNewFlameTrails", 0, random( -2, 2))
		FR7X A 1 BRIGHT A_SpawnItem("RedFlareSmall",0,0)
		TNT1 A 0 A_CustomMissile ("OldschoolRocketSmokeTrail2", 2, 0, random (160, 210), 2, random (-30, 30))
		TNT1 A 0 A_SpawnItem ("CoolAndNewFlameTrails")
		FR7X B 1 BRIGHT A_SpawnItem("RedFlareSmall",0,0)
		Loop
	}
}

Actor ShotgunTriShotReady : Inventory { inventory.maxamount 1 }
Actor ShotgunExplosiveReady : Inventory { inventory.maxamount 1 }