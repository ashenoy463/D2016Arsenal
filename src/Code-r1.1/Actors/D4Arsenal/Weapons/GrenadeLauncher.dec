ACTOR D4GrenadeLauncher : D4WeaponBase replaces GrenadeLauncher
{
	Weapon.AmmoType1 "D4Rockets"
	Weapon.AmmoType2 "VirtualGrenade"
	Weapon.AmmoGive1 4
	Weapon.AmmoGive2 0
	Weapon.AmmoUse1 0
	Weapon.AmmoUse2 0
    Inventory.PickupSound "ROCKPKUP"
	Inventory.PickupMessage "Picked up a Grenade Launcher. From UAC Explosive Division with love."
	Obituary "%o was exploded by %k's grenades."
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOALERT
	+WEAPON.NOAUTOAIM
	Tag "Grenade Launcher"
	States
	{
	WReady:
		TNT1 A 0 A_JumpIfInventory("Grabbing_A_Ledge",1,"LedgeClimb")
		TNT1 A 0 A_JumpIfInventory("IsUpgradingWeapons",1,"UpgradeWeapons")
		TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		TNT1 A 0 A_JumpIfInventory("Fistting",1,"DoPunch")
		TNT1 A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		TNT1 A 0 A_JumpIfInventory("StartUpgrader",1,"StartUpgrader")
		TNT1 A 0 A_JumpIfInventory("Taunting",1,"TauntL")
		//TNT1 A 0 A_JumpIfInventory("Reloading",1,"ChangeAltMode")
		TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		GLRG A 1 A_WeaponReady
		Loop
		
	StartUpgrader:
		TNT1 A 0 A_TakeInventory("StartUpgrader",1)
		TNT1 A 0 A_GiveInventory("UpgradeGrenadeLauncher",1)
		TNT1 A 0 A_SelectWeapon("D4WeaponUpgrader")
		Goto WReady
	
	Deselect:
		TNT1 A 0 A_Takeinventory("NoParkour",1)
		TNT1 A 0 A_TakeInventory("IsUpgradeableWeapon",1)
		GLRS EDCBA 1
		GLRS A 0
		Goto DeselectSequence
		
	Select: 
		TNT1 A 0 A_GiveInventory("SwitchWeaponTaker", 1)
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_Giveinventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_GiveInventory("IsUpgradeableWeapon",1)
		TNT1 A 0 A_JumpIfInventory("IsDemonPlayer", 1, "SelectSpecial")
		TNT1 A 0 A_JumpIfInventory("JustStartedGame",1,"SelectSpecial")
	    TNT1 A 0 A_JumpIfInventory("GoFatality", 1, "SelectSpecial")
		GLRS A 0
		Goto SelectSequence

	Ready:
		TNT1 A 0 A_GunFlash
		TNT1 A 0 A_PlaySound("Weapon/Switch")
	SelectAnimation:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		GLRS ABCDE 1
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
	AmmoChecker:
		TNT1 A 0 A_JumpIfInventory("VirtualGrenade",1,"WReady")
		Goto LoadRoundReady
		
	Fire:
		TNT1 A 0 A_JumpIfInventory("VirtualGrenade",1,"FireProcced")
		Goto LoadRound
	FireProcced:
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		TNT1 A 0 A_GunFlash("FirePrimary")
		GLRF A 1 BRIGHT A_PlaySound("GrenadeLauncher/Fire")
		TNT1 A 0 A_TakeInventory("VirtualGrenade",1)
		Goto FireEnd
		
	FirePrimary:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
		RLNG A 0 BRIGHT A_FireCustomMissile("Alerter")
		TNT1 AAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-16,16), 0, -1, random(-9,9))
		TNT1 A 0 A_FireCustomMissile("GrenadeMissileBreaksGlass", 0, 0, 0, 0)
		TNT1 A 0 A_SetBlend("Yellow",0.1,8)
		TNT1 A 0 A_JumpIfInventory("HasUpgradeGrenadeLaucherDamage",1,"GrenadeMissileUpgraded")
		Goto GrenadeMissile
		
		GrenadeMissile:
			TNT1 A 1 A_FireCustomMissile("D4GrenadeMissile", 0, 1, -1, 0)
			Goto Flash
		GrenadeMissileUpgraded:
			TNT1 A 1 A_FireCustomMissile("D4GrenadeMissileUpgraded", 0, 1, -1, 0)
			Goto Flash
		
	AltFire:
		TNT1 A 0 A_JumpIfInventory("HasUpgradeGrenadeLaucherAltFire",1,"AltFireCheckForAmmo")
		Goto NoUpgrades
	
	AltFireCheckForAmmo:
		TNT1 A 0 A_JumpIfInventory("D4Rockets",1,"AltFireProcced")
        Goto GiveFakeRocket
	AltFireProcced:
		TNT1 A 0 A_JumpIfInventory("GLGasTimer",1,"ModNotReady")
		TNT1 A 0 A_GunFlash("FireSecondary")
        TNT1 A 0 A_GiveInventory("NoParkour", 1)
		GLRF A 1 BRIGHT A_PlaySound("GrenadeLauncher/Fire")
		TNT1 A 0 A_TakeInventory("D4Rockets",1)
		Goto FireEnd
		
	FireSecondary:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
		RLNG A 0 BRIGHT A_FireCustomMissile("Alerter")
		TNT1 AAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-16,16), 0, -1, random(-9,9))
		TNT1 A 0 A_FireCustomMissile("GrenadeMissileBreaksGlass", 0, 0, 0, 0)
		TNT1 A 0 A_SetBlend("Yellow",0.1,8)
		TNT1 A 0 A_GiveInventory("GLGasTimer",52)
		TNT1 A 0 ACS_NamedExecuteAlways("GrenLaunchGasTimer",0,0,0,0)
		TNT1 A 0 A_JumpIfInventory("HasUpgradeGrenadeLaucherDamage",1,"GasGrenadeUpgraded")
		Goto GasGrenade
		
		GasGrenade:
			TNT1 A 1 A_FireCustomMissile("D4GasGrenade", 0, 1, -1, 0)
			Goto Flash			
		GasGrenadeUpgraded:
			TNT1 A 1 A_FireCustomMissile("D4GasGrenadeUpgraded", 0, 1, -1, 0)
			Goto Flash
		
	FireEnd:
		TNT1 A 0 A_SetPitch(-8.0 + pitch)
		TNT1 A 0 A_ZoomFactor(0.9)
		GLRF B 1 BRIGHT
		GLAN A 0 A_ZoomFactor(1.0)
		GLRF CDEEEDDC 1 A_SetPitch(1.0 + pitch)
		GLRF CFF 1
		GLRG BCD 2
		GLRG A 1
		TNT1 A 0 A_Refire
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		TNT1 A 0 A_JumpIfInventory("VirtualGrenade",1,1)
		Goto AmmoChecker
		TNT1 A 0
		Goto WReady
		
	NoAmmo:
		GLRG A 1 A_playsound("weapons/empty")
        TNT1 A 0 A_TakeInventory("NoParkour", 1)
		Goto WReady
		
	Spawn:
		D4W5 B -1
		Stop
	
	// Ammo Givers		
	LoadRoundReady:
		TNT1 A 0 A_JumpIfInventory("VirtualGrenade",1,"WReady")
		TNT1 A 0 A_JumpIfInventory("D4Rockets",1,1)
        Goto WReady
		TNT1 A 0 A_TakeInventory("D4Rockets", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("VirtualGrenade", 2)
		Goto LoadRoundReady
		
	LoadRound:
		TNT1 A 0 A_JumpIfInventory("VirtualGrenade",2,"FireProcced")
		TNT1 A 0 A_JumpIfInventory("D4Rockets",1,1)
        Goto NoAmmo
		TNT1 A 0 A_TakeInventory("D4Rockets", 1, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("VirtualGrenade", 2)
		Goto LoadRound
		
	GiveFakeRocket:
		TNT1 A 0 A_JumpIfInventory("D4Rockets",1,"AltFireProcced")
		TNT1 A 0 A_JumpIfInventory("VirtualGrenade",2,1)
        Goto NoAmmo
		TNT1 A 0 A_TakeInventory("VirtualGrenade", 2, TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("D4Rockets", 1)
		Goto GiveFakeRocket

// ----------------------------------------------------------------------------------------------------------------------------------------------------------
//
// Flash States
//
// ----------------------------------------------------------------------------------------------------------------------------------------------------------

	PunchFlash:
		GLRS ECA 1
		Goto Flash
	
	KickFlash:
		GLRS EDCB 1
		GLRS A 8
		GLRS BCDE 1
		Goto Flash

	AirKickFlash:
		GLRS EDCB 1
		GLRS A 10
		GLRS BCDE 1
		Goto Flash
		
	SlideKickStartFlash:
		GLRS EDCB 1
		GLRS A 17
	SlideKickEndFlash:
		GLRS A 5 
		GLRS BCDE 1
		Goto Flash

	TauntFlash:
		GLRT ABCDE 1
		GLRT F 33
		GLRT EDCBA 1
		Goto Flash
		
	GrenadeFlash:
		GLRS ECA 1
		Goto Flash
	}
}

Actor VirtualGrenade : Ammo 
{
	+FLOORCLIP
	-TELESTOMP
	Radius 20
	Height 16
	Inventory.Amount 0
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
	+INVENTORY.IGNORESKILL
	Inventory.Icon "D4W5B0"
	Tag "Grenade"
	Scale 0.45
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

// --------------------
// Primary Related

Actor D4GrenadeMissile
{
	var int user_timer;
	PROJECTILE
	Radius 4
	Height 9
	Damage (random(60, 60))
	Speed 35
	Mass 0
	Gravity 0.45
	ReactionTime 135
	DamageType Kick
	BounceType "Hexen"
	BounceSound "GrenadeLauncher/GrenBounce"
	BounceFactor 0.35
	WallBounceFactor 0.35
	+CANBOUNCEWATER
	-NOGRAVITY
	+FORCERADIUSDMG
    +BLOODSPLATTER
	+SKYEXPLODE
	+BOUNCEONWALLS
	+BOUNCEONCEILINGS
	+USEBOUNCESTATE
	+EXPLODEONWATER
	Scale 0.5
	Translation "80:111=%[0,0,0]:[1,0.5,0]"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0,7,0,1)
		GRNL A 1 Bright A_CustomMissile("RocketSmokeTrail52", 2, 0, random (70, 110), 2, random (0, 360))
		Wait
		
		Bounce:	//once it bounces, it starts the fuse that lasts very little
			GRNL A 0 A_ChangeFlag("USEBOUNCESTATE",0)
			//GRNL A 0 A_PrintBold("Bounce",0.2)
			GRNL A 0 A_CustomMissile("RocketSmokeTrail52", 2, 0, random (70, 110), 2, random (0, 360))
			GRNL A 1 A_PlaySound("Special/Grenade/FragArm",6,0.6)
			Goto Counter
	Counter:
		GRNL A 0 A_JumpIf(user_timer>=20,"Deather")
		GRNL A 0 A_SetUserVar(user_timer,user_timer+1)
		GRNL A 1 A_CustomMissile("RocketSmokeTrail52", 2, 0, random (70, 110), 2, random (0, 360))
		Loop
		
	Deather:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_StopSound(6) //stops the "arm" sound
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Jump(256,"Death.GrenadeTimerOff")	//needed for inheritance jumps
		GRNL A 1
		Goto Death.GrenadeTimerOff
		
	XDeath:
	TNT1 A 0 A_Recoil(1)
	TNT1 A 0 A_SetAngle(random (90,-90) + angle)
	GRNL A 1 ThrustThingZ(0,20,0,1)
	Goto Bounce
		
	Death:
	Death.Slime:
	Death.Fire:
	Death.Bullet:
	Death.Shotgun:
	Death.SSG:
	Death.Cutless:
	Death.Shrapnel:
	    TNT1 A 0
		GRNL A 0 A_JumpIf(user_timer>=20,"Deather")
		GRNL A 0 A_SetUserVar(user_timer,user_timer+1)
		GRNL A 1
		Loop
		
	Death.GrenadeTimerOff:
		Goto ExplosionState
		
	ExplosionState:
		TNT1 A 0 A_SpawnItemEx ("D4GLExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		Goto ContinueExplosion
		
	ContinueExplosion:
		TNT1 A 0 A_StopSound(6) //stops the "arm" soundTNT1 A 0
		TNT1 A 1 A_SpawnItemEx ("Footstep91",0,0, 40,0,0,0,0,SXF_NOCHECKPOSITION,0)
		TNT1 A 1
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
        EXPL A 0 Radius_Quake (3, 8, 0, 15, 0)//(intensity, duration, damrad, tremrad, tid)
		TNT1 A 0 A_SpawnItemEx ("ExplosionSplashSpawner", 0, 0, -20)
        TNT1 A 0 A_SpawnItemEx ("DetectFloorCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("DetectCeilCrater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	    TNT1 A 0 A_SpawnItemEx ("UnderwaterExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAAAAAAAAA 0 A_CustomMissile ("BDExplosionparticlesBig", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 AAAAA 0 A_CustomMissile ("BDExplosionparticles2", 0, 0, random (0, 360), 2, random (0, 90))
		TNT1 AAAA 0 A_CustomMissile ("MediumExplosionFlames", 0, 0, random (0, 360), 2, random (0, 360))		
		TNT1 AA 0		
        TNT1 A 0 A_PlaySound("Explosion", 1)
		TNT1 A 0
		TNT1 A 0 A_PlaySound("FAREXPL", 3)
		//TNT1 AAAAAAAAAAAAA 3 A_CustomMissile ("HeavyExplosionSmoke", 2, 0, random (0, 360), 2, random (0, 360))
		Stop	
	}
}

Actor D4GrenadeMissileUpgraded : D4GrenadeMissile
{
	States
	{
	ExplosionState:
		TNT1 A 0 A_SpawnItemEx ("D4GLExplosionUpgraded",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
		Goto ContinueExplosion
	}
}

Actor D4GLExplosion: RocketExplosion
{
	+FORCERADIUSDMG
	States
	{
    Spawn:
		TNT1 A 0
		TNT1 A 3
		TNT1 A 0 A_Explode(75,160)
		TNT1 A 0 A_SpawnItem("WhiteShockwaveBig")
        Stop
	}
}

Actor D4GLExplosionUpgraded : D4GLExplosion
{
	States
	{
    Spawn:
		TNT1 A 0
		TNT1 A 3
		TNT1 A 0 A_Explode(100,280)
		TNT1 A 0 A_SpawnItem("WhiteShockwaveBig")
        Stop
	}
}	
		
// -----------------
// Secondary Related

Actor D4GasGrenade
{
	PROJECTILE
	-NOGRAVITY
	+FORCERADIUSDMG
	+EXTREMEDEATH
	Radius 4
	Height 9
	Damage 15
	Speed 35
	Mass 0
	Gravity 0.1
	ReactionTime 20
	Scale 0.7
	Translation "80:111=%[0,0,0]:[1,0.5,0]", "176:192=%[0,0,0]:[1,2,0]"
	States
	{
	Spawn:
		GRNL A 1 Bright A_CustomMissile("RocketSmokeTrail52", 2, 0, random (70, 110), 2, random (0, 360))
		GRNL A 0 A_CountDown
		Loop
	Death:
		TNT1 A 0 A_PlaySound("GrenadeLauncher/SmokeExplosion")
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),     0,   0,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),    32,   0,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),   -32,   0,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),     0,   32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),    32,   32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),   -32,   32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),     0,  -32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),    32,  -32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4Gas", random(0,-15),   -32,  -32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 10
		Stop
	}
}

Actor D4GasGrenadeUpgraded : D4GasGrenade
{
	States
	{
	Death:
		TNT1 A 0 A_PlaySound("GrenadeLauncher/SmokeExplosion",CHAN_AUTO,1.0,false,0.9)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),     0,   0,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),    32,   0,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),   -32,   0,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),     0,   32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),    32,   32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),   -32,   32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),     0,  -32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),    32,  -32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("D4GasSmart", random(0,-15),   -32,  -32,  0,0,0, 0, SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 10
		Stop
	}
}

Actor D4Gas
{
	+NOINTERACTIoN
	RenderStyle Add
	+FORCEXYBILLBOARD
	+BLOODLESSIMPACT
	+PAINLESS
	+BRIGHT
	Scale 1.4
	Alpha 0.99
	Translation "0:255=%[0,0,0]:[0.4,1,0]"
	States
	{
	Spawn:
		X101 A 0 NODELAY A_Jump(220,2,3,4,5,6,7,8,10,11,12,13,14,15,16,17,18,19)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80)
	Outer:
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		Stop
	}
}

Actor D4GasSmart
{
	+NOINTERACTIoN
	RenderStyle Add
	+FORCEXYBILLBOARD
	+BLOODLESSIMPACT
	+PAINLESS
	+BRIGHT
	+MTHRUSPECIES
	Scale 1.4
	Alpha 0.99
	Translation "0:255=%[0,0,0]:[0.4,1,0]"
	States
	{
	Spawn:
		X101 A 0 NODELAY A_Jump(220,2,3,4,5,6,7,8,10,11,12,13,14,15,16,17,18,19)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80,0)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80,0)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80,0)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80,0)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80,0)
		X101 ABCDEFGHIJKLMNOP 2 A_Explode(5,80,0)
	Outer:
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		X101 ABCDEFGHIJKLMNOP 2 A_FadeOut(0.03)
		Stop
	}
}