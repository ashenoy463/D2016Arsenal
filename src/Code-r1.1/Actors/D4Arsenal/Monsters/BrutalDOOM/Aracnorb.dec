// ===========================================================
// Updated code from canceled Monster Expansion 1.3 Mod
// ----------------------------------------------------
//
// I do not understand why the code and sprites are present 
// in Brutal DOOM v21 but are in disuse, which means that 
// Mark was going to implement them, anyway this code revives 
// the Aracnorbs and brings them back to BDv21.
//
// -XV117
// ===========================================================

//Actor D4Aracnorb : Aracnorb replaces Aracnorb
Actor D4Aracnorb // replaces Aracnorb
{
  	//$Title "Aracnorb"
  	//$Category "DOOM 2016 Arsenal - Monsters"
  	MONSTER
  	+NOGRAVITY
  	+FLOAT
  	+FLOATBOB
  	+FORCEXYBILLBOARD
  	+DONTHARMSPECIES  
  	+MISSILEMORE         
  	Health 485
  	Radius 31//56spriteW(110%=62)
  	Height 66//60spriteH(110%=66)
  	Scale 1.1
  	Mass 485
  	Speed 10
  	FloatSpeed 5
  	PainChance 128
	var int user_healto;
	var int user_staggercount;
  	Species "Demon"
  	SeeSound "aracnorb/sight"
  	ActiveSound "baby/active"
  	PainSound "baby/pain"
  	DeathSound "aracnorb/death"
  	MeleeSound "demon/melee"
  	Obituary "%o suffered psychic trauma from an aracnorb's brainwaves."
  	HitObituary "%o had %p skull chewed by an aracnorb."
  	DropItem "DemonStrengthRune" 8
  	damagefactor "Shrapnel", 3.0
	Tag "Aracnorb"
	WoundHealth 50
	PainChance "GloryKill", 255
	PainChance "ChainsawPinata", 255
	PainChance "Stun", 255
	States
	{	
  	Spawn:
    		ACNB A 1 A_Look
    		Loop

  	See:
    		ACNB A 2 A_Chase
    		Loop

  	Melee:
		TNT1 A 0 A_Playsound("caco/melee")
    		ACNB A 3 A_FaceTarget
		TNT1 A 0 A_Recoil(-1)
    		ACNB B 3 
		TNT1 A 0 A_Recoil(-1)
    		ACNB C 3
		TNT1 AAA 0 A_CustomMissile("DemonAttack",40,0,0,0)
    		ACNB B 3 
		ACNB A 5
    		Goto See

  	Missile:
    		ACNB B 8 A_FaceTarget
    		ACNB C 4 Bright
    		ACNB B 2 A_CustomMissile("WeakGreenPlasmaBall", 34, 0, 0, 0)
    		ACNB D 0 A_MonsterRefire(220, "See")
    		Goto Missile
	
  	Pain.Killme:
	Pain.Taunt:
		TNT1 A 0
		TNT1 A 0 HealThing(1)
		Goto Missile
		
  	Pain:
    		ACNF I 2
    		ACNF I 2 A_Pain
    		Goto See

  	Pain.Kick:
  	Pain.ExtremePunches:
		TNT1 A 0 A_Recoil(10)
    		ACNF I 6 A_FaceTarget
    		ACNF I 6 A_Pain
    		Goto See   

  	Pain.HeavyImpact:
		TNT1 A 0 A_FaceTarget
		ACNF I 6 A_Recoil(5)
    		ACNF I 6 A_Pain
		ACNF I 10
    		Goto See    
 
  	Pain.Avoid: 
	  	TNT1 A 0
	  	TNT1 A 0 A_Jump(255, "AvoidLeft", "AvoidRight")
	  	Goto AvoidLeft
	  
	AvoidLeft:
	  	ACNB A 1 A_FaceTarget
	 	TNT1 A 0 ThrustThing(angle*256/360+192, 15, 0, 0)
    		ACNB A 5 A_FaceTarget
    		Goto Missile

	AvoidRight:
	  	ACNB A 1 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+64, 15, 0, 0)
    		ACNB A 5 A_FaceTarget
    		Goto Missile
    
	Death:
    		ACNB D 0 A_ChangeFlag("FLOATBOB",0)	
		ACNB D 8 A_Scream
    		TNT1 AAAAA 0 A_CustomMissile ("CeilBloodLauncherLong", 0, 0, random (0, 360), 2, random (50, 130))
 	  	TNT1 AAAA 0 A_CustomMissile ("Guts", 32, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAA 0 A_CustomMissile ("Brains1", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains2", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains3", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains4", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains5", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 AAAAAA 0 A_CustomMissile ("SmallBrainPiece", 5, 0, random (0, 360), 2, random (60, 140))
	  	TNT1 AAAAAAA 0 A_CustomMissile ("BloodMist", 50, 0, random (0, 360), 2, random (60, 90))
    		TNT1 AA 0 A_CustomMissile ("XDeathSpiderNew1", 42, 0, random (0, 360), 2, random (60, 90))
    		TNT1 A 0 A_CustomMissile ("XDeathSpiderNew2", 42, 0, random (0, 360), 2, random (60, 90))
		ACNB E 6 A_NoBlocking
		ACNB FG 8
		TNT1 A 0 A_SpawnItem ("GrowingBloodPool")
		ACNB H -1 		
    		Stop

	Death.SuperPlasma:
  	Death.Plasma:
  	Death.Plasma2:
  	Death.GreenFire:  
    		ACNB D 0 A_ChangeFlag("FLOATBOB",0)	 
    		TNT1 A 0 A_Jump(50, "XDeath") 
    		TNT1 A 0 A_Scream
    		TNT1 A 0 A_NoBlocking
    		TNT1 AAA 0 A_CustomMissile ("Brutal_FlyingBlood", 50, 0, random (0, 360), 2, random (0, 160))
	  	TNT1 AA 0 A_CustomMissile ("XDeathBurnedMeat3", 32, 0, random (0, 360), 2, random (0, 160))
		EXPL AA 0 A_CustomMissile ("ExplosionSmoke", 32, 0, random (0, 360), 2, random (0, 360))
    		CARB JJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJ 7 A_CustomMissile ("PlasmaSmoke", 15, 0, random (0, 360), 2, random (70, 110))
    		CARB J -1
    		Stop

	Death.Fire:
		TNT1 A 0
    		ACNB D 0 A_ChangeFlag("FLOATBOB",0)	  
    		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAA 0 A_CustomMissile ("PEPart2", 32, 0, random (0, 360), 2, random (0, 160))
		ACNB DDDD 2 A_CustomMissile ("FlameTrails", 45, 0, random (0, 360), 2, random (50, 130))
		ACNB EEEE 2 A_CustomMissile ("FlameTrails", 30, 0, random (0, 360), 2, random (50, 130))
		ACNB FFFF 2 A_CustomMissile ("FlameTrails", 25, 0, random (0, 360), 2, random (50, 130))
		ACNB GGGG 2 A_CustomMissile ("FlameTrails", 25, 0, random (0, 360), 2, random (50, 130))
		TNT1 A 0 A_Playsound("props/redfire")
		Goto Burns

	Burns:
		TNT1 A 0 A_GiveInventory("BrutalCounter")
		CARB JJ 2 A_SpawnItem("YellowFlare",0,20)
		ID22 F 0 A_CustomMissile ("FlameTrails", 20, 0, random (0, 360), 2, random (70, 110))
		CARB JJ 2 A_SpawnItem("YellowFlare",0,20)
		ID22 F 0 A_CustomMissile ("FlameTrails", 20, 0, random (0, 360), 2, random (70, 110))
		ID22 F 0 A_CustomMissile ("PlasmaSmoke", 30, 0, random (0, 360), 2, random (70, 110))
		TNT1 A 0 A_JumpIfInventory("BrutalCounter", 30, "Smokes")
		Loop

	Smokes:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("BrutalCounter", 60, "Rest")
		TNT1 A 0 A_GiveInventory("BrutalCounter")
		CARB J 5 A_CustomMissile ("PlasmaSmoke", 30, 0, random (0, 360), 2, random (70, 110))
		Loop	

	Rest:	
		CARB J -1
		Stop    
  	Death.Cut:
	Death.Saw:
    		ACNB D 0 A_ChangeFlag("FLOATBOB",0)	 
    		TNT1 A 0 A_Jump(50, "XDeath")     	
		TNT1 AAA 0 A_CustomMissile ("Guts2", 14, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAA 0 A_CustomMissile ("CeilBloodLauncher", 40, 0, random (0, 360), 2, random (50, 130))
	  	TNT1 AAAA 0 A_CustomMissile ("XDeath1", 10, 0, random (0, 360), 2, random (0, 160))
	  	TNT1 AAAA 0 A_CustomMissile ("XDeath2", 10, 0, random (0, 360), 2, random (0, 160))
	  	TNT1 AAAAAAAAAA 0 A_CustomMissile ("PlayerFlyingBlood", 50, 0, random (0, 360), 2, random (0, 160))
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		ACNB JKLM 4 
		TNT1 AAAA 0 A_CustomMissile ("Brains1", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains2", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains3", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains4", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 A 0 A_CustomMissile ("Brains5", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 AAAAAA 0 A_CustomMissile ("SmallBrainPiece", 5, 0, random (0, 360), 2, random (60, 140))
		TNT1 AAAAAAAAAA 0 A_CustomMissile ("Guts2", 24, 15, random (0, 360), 2, random (0, 160))
		TNT1 A 0 A_SpawnItem ("GrowingBloodPool")
		ACNB NNNNN 1	A_CustomMissile ("Brutal_LiquidBlood2", 28, 0, random (0, 360), 2, random (30, 60))	
		ACNB OOOOO 1	A_CustomMissile ("Brutal_LiquidBlood2", 21, 0, random (0, 360), 2, random (30, 60))	
		ACNB PPPPPPPP 1	A_CustomMissile ("Brutal_LiquidBlood2", 14, 0, random (0, 360), 2, random (30, 60))	
		ACNB P -1
		Stop  
  
	XDeath:
	Death.XDeath:
	Death.Blast:
	Death.SSG:
	Death.ExplosiveImpact:	
	Death.Crush:		
    		ACNB D 0 A_ChangeFlag("FLOATBOB",0)	 	
    		ACNB I 2
		TNT1 A 0 A_Playsound("misc/gibbed",3)    
		TNT1 AAA 0 A_CustomMissile ("Guts2", 14, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAA 0 A_CustomMissile ("CeilBloodLauncher", 40, 0, random (0, 360), 2, random (50, 130))
	  	TNT1 AAAAAA 0 A_CustomMissile ("XDeath1", 10, 0, random (0, 360), 2, random (0, 160))
	  	TNT1 AAAAAA 0 A_CustomMissile ("XDeath2", 10, 0, random (0, 360), 2, random (0, 160))
    		TNT1 AAAA 0 A_CustomMissile ("Aracnorb_Brain", 32, 0, random (0, 360), 2, random (0, 160))	  
	  	TNT1 AAAAAAAAAA 0 A_CustomMissile ("PlayerFlyingBlood", 50, 0, random (0, 360), 2, random (0, 160))
		TNT1 A 0 A_Scream
		TNT1 A 0 A_NoBlocking
		POSS STUVW 3
		TNT1 A 0 A_SpawnItem ("GrowingBloodPool")
		POSS X -1
		Stop  

  	Death.Fatality:
	  	TNT1 A 0 A_GiveToTarget("FatalityRewardAracnorb", 1)
		Fatalized:
		TNT1 A 0 A_Pain
		TNT1 A 0 A_GiveToTarget("GoSpecial", 1)
	  	TNT1 A 0 A_GiveToTarget("GoFatality", 1)
    	TNT1 A 0 A_GiveToTarget("AracnorbFatality", 1)
    	TNT1 A 0 
		Stop		
				    
  	Raise:
    		ACNB HGFEDA 8
    		ACNB A 0 A_ChangeFlag("FLOATBOB",1)
		Goto See

	// DOOM 2016 Arsenal Stuff
	Pain.Stun:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag(NOPAIN,1)
		TNT1 A 0 A_Pain
		ACNF IIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIII 5 A_GiveInventory(StunFXSpawner) // 5 Seconds
		TNT1 A 0 A_ChangeFlag(NOPAIN,0)
		Goto See
	
	Death.Stun:
		TNT1 A 0
		Goto Death.Plasma
		
	Wound:
		ACNG C 0 A_SetUserVar(user_healto,65)
		"####" "#" 0 A_JumpIfInventory("DisabledGloryKill",1,"See",AAPTR_PLAYER1) // Checks if are disabled the Glory Kill System
		"####" "#" 0 A_GiveInventory("GloryKillable",1)
		"####" "#" 0 Thing_SetTranslation(0,1)
		"####" "#" 0 A_Pain
		"####" "#######" 5 Bright
		"####" "#" 0 A_Pain
		"####" "#######" 5 Bright
		"####" "#" 0 A_Pain
		"####" "#######" 5 Bright
		"####" "#" 0 A_Pain
		"####" "#######" 5 Bright
		"####" "#" 0 Thing_SetTranslation(0,2)
		"####" "#" 0 A_TakeInventory("GloryKillable",32767)
		"####" "#" 0 A_SetUserVar(user_staggercount,user_staggercount+1)	//to keep track how many times has been staggered
		"####" "#" 0 A_JumpIf(user_staggercount>=3,"DeathByStagger")	//if has been staggered 3 times, when the 3rd ends, it means death
		"####" "#" 0 ACS_NamedExecuteAlways("HealMonAfterStagger",0,user_healto,0,0) //second numeric value is how much it will heal
		Goto See
		
	DeathByStagger:
		"####" "#" 0 A_Die
		Goto Death
		
	Death.ChainsawPinata:	
		ACNG "#" 0
		"####" A 0 A_GiveToTarget("ConfirmedGKill",1)
		"####" A 0 A_ChangeFlag("Solid",0)
		"####" A 0 A_Stop
		"####" A 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE) 
		"####" A 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE ) 
		"####" AAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE )
		"####" A 0 A_Pain
		"####" "#" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" BBBBBB 1 A_Warp(AAPTR_TARGET,-50,random(-1,1),random(-1,1),180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		"####" "##" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" BBBBBB 1 A_Warp(AAPTR_TARGET,-50,random(-1,1),random(-1,1),180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		"####" "##" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" "#" 0 A_Pain
		"####" BBBBB 1 A_Warp(AAPTR_TARGET,-50,random(-1,1),random(-1,1),180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		"####" "##" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" BBBBB 1 A_Warp(AAPTR_TARGET,-50,random(-1,1),random(-1,1),180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		"####" "##" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" "#" 0 A_Pain
		"####" BBBBB 1 A_Warp(AAPTR_TARGET,-50,random(-1,1),random(-1,1),180,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
		"####" "##" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" "#" 0 A_Scream
		"####" A 0 A_GiveToTarget("SuccessfulExecutionToken",1)
		"####" A 0 A_SpawnItemEx("ChainsawExecutionPinata",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto Death.Saw

	Death.GloryKill:
		ACNG A 0
		"####" "#" 0 A_JumpIfInTargetInventory("GKFatalityMode",1,"GKFatalityMode") // Turn to Fatality Execution if the cvar is enabled
		"####" "#" 0 A_GiveToTarget("ConfirmedGKill",1)
		"####" "#" 0
		"####" A 0 A_ChangeFlag("Solid",0)
		"####" A 0 Thing_SetTranslation(0,2)
		"####" A 0 A_Stop
		"####" A 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION) 
		"####" A 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_COPYINTERPOLATION ) 
		"####" A 0 A_JumpIfInTargetInventory("InstantGloryKillToken",1,"GloryEnd")
		"####" A 0 A_JumpIfInTargetInventory("GloryKill1Token",1,"GloryEnd1")
		"####" A 0 A_JumpIfInTargetInventory("GloryKill2Token",1,"GloryEnd2")
		"####" A 0 A_JumpIfInTargetInventory("GloryKill3Token",1,"GloryEnd3")
	GloryEnd1:
		"####" AAAAAAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION )
		"####" A 0 A_Pain
		"####" "#" 0 A_PlaySound("GloryKill/BoneSnap",7,1,false,2.22)
		"####" "#" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" BBBBBB 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION )
		"####" AAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION ) 
		"####" "#" 0 A_PlaySound("GloryKill/BoneSnap",6,1,false,2.22)
		"####" "#" 0 A_Jump(256,"GloryEnd")
		"####" "#" 1
		Goto GloryEnd
	GloryEnd2:
		"####" AAAAAAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION )
		"####" A 0 A_Pain
		"####" "#" 0 A_PlaySound("GloryKill/BoneSnap",7,1,false,2.22)
		"####" "#" 0 A_SpawnItem("MuchBlood", 0, 40)
		"####" BBBBBB 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION )
		"####" AAAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION ) 
		"####" "#" 0 A_PlaySound("GloryKill/BoneSnap",6,1,false,2.22)
		"####" "#" 0 A_Jump(256,"GloryEnd")
		"####" "#" 1
		Goto GloryEnd
	GloryEnd3:
		"####" AAAA 1 A_Warp(AAPTR_TARGET,-50,0,0,180,WARPF_NOCHECKPOSITION|WARPF_WARPINTERPOLATION )
		"####" "#" 0 A_PlaySound("GloryKill/BoneSnap",7,1,false,2.22)
		"####" A 0 A_ChangeVelocity(-8,0,10,CVF_RELATIVE)
		"####" "#" 0 A_Jump(256,"GloryEnd")
		"####" "#" 1
		Goto GloryEnd
		
	GloryEnd:	//depending on the monster, is what and how much will spawn
		TNT1 A 0 A_JumpIfInTargetInventory("PowerStrength",1,"BerserkBonus") // Bonus for Berserk Executors
		TNT1 A 1 A_SpawnItemEx("GloryKillPinataMedium",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto XDeath

	BerserkBonus:
		TNT1 A 1 A_SpawnItemEx("GloryKillBerserkPinataMedium",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto XDeath
		
	GKFatalityMode:
		TNT1 A 0 A_JumpIfInTargetInventory("PowerStrength",1,"GKFatalityBerserk") // Bonus for Berserk Executors
		TNT1 A 0 A_SpawnItemEx("GloryKillPinataMedium",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto Fatalized

	GKFatalityBerserk:
		TNT1 A 0 A_SpawnItemEx("GloryKillBerserkPinataMedium",0,0,32,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto Fatalized
	}
}

ACTOR AracnorbFatality : Inventory
{
Inventory.MaxAmount 1
}

ACTOR Aracnorb_Brain
{
    	Radius 8
    	Height 8
    	Speed 16
    	Scale 1.5
	Mass 1
    	+NOBLOCKMAP
	+MISSILE
    	+NOTELEPORT
    	+MOVEWITHSECTOR
    	+CLIENTSIDEONLY
    	+RIPPER
    	+BLOODLESSIMPACT 
    	+DOOMBOUNCE
	+SKYEXPLODE
    	BounceFactor 0.5
	damagetype Blood
    	SeeSound "misc/xdeath4"
    	DeathSound "misc/xdeath1"
	Decal BloodSuper
    	States
    	{
    	Spawn:
		TNT1 A 0 A_JumpIf(waterlevel > 1, "Splash")
		ID22 A 2 A_CustomMissile("BloodTrails",0,0,180,2)
		ID22 B 2 A_CustomMissile("BloodTrails",0,0,180,2)
		ID22 C 2 A_CustomMissile("BloodTrails",0,0,180,2)
		ID22 D 2 A_CustomMissile("BloodTrails",0,0,180,2)
		Loop

	Death:    
		ID22 F 1 A_SpawnItem("Brutal_FlyingBlood",0,5,0,1)
		ID22 F -1
		Stop

	Splash:
		TNT1 A 0
		Stop
    }
}

Actor StompedAracnorb: DeadSpider
{
	+NOBLOOD
	Health 999999999999
	States
	{
	Spawn:
		FTAC P -1
		Stop    
 	}
}